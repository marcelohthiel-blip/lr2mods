label intro_standing_finger(the_girl, the_location, the_object):
    $ standing_finger.redraw_scene(the_girl)
    "You stand behind [the_girl.title] and put your arms around her, pulling her close against you."
    if the_girl.vagina_available:
        "You don't waste any time pushing your hand between her legs, teasing her cute, exposed pussy with your fingers."
        $ play_moan_sound()
        "She moans quietly as you slide two fingers inside her wet hole."
    else:
        $ the_item = the_girl.outfit.get_lower_top_layer
        if the_item:
            "You don't waste any time pushing your hand between her legs, sliding it under her [the_item.display_name] to reach her pussy."
            "You run a finger over it, teasing it first."
            $ play_moan_sound()
            "She moans quietly as you slide two fingers into her wet hole."
            $ the_item = None
        else:
            $ play_moan_sound()
            "She moans quietly as you slide two fingers inside her wet hole."
    return

label taboo_break_standing_finger(the_girl, the_location, the_object):
    $ standing_finger.redraw_scene(the_girl)
    "You kiss [the_girl.title]'s neck from behind, distracting her from your hand sliding along her inner thigh and towards her crotch."
    if the_girl.effective_sluttiness(standing_finger.associated_taboo) > standing_finger.slut_cap:
        if the_girl.vagina_available:
            "She gasps as you brush her sensitive pussy. She spreads her legs for you, giving you easy access."
        else:
            $ the_item = the_girl.outfit.get_lower_top_layer
            if the_item:
                "You slide your hand under her [the_item.display_name] and make her gasp as you brush her sensitive pussy."
                "She spreads her legs and leans back against you, giving you easy access."
                $ the_item = None
            else:
                "She gasps as you brush her sensitive pussy. She spreads her legs for you, giving you easy access."
        $ the_girl.call_dialogue(f"{standing_finger.associated_taboo}_taboo_break")
        "You move your hand over her clit and feel her shiver in response to your touch."

    else:
        if the_girl.vagina_available:
            "She starts as you brush her sensitive pussy. She grabs your wrist and stops you from moving any farther."
        else:
            $ the_item = the_girl.outfit.get_lower_top_layer
            if the_item:
                "She starts as you slide your hand under her [the_item.display_name]. She grabs your wrist and stops you from moving any farther."
                $ the_item = None
            else:
                "She starts as you brush her sensitive pussy. She grabs your wrist and stops you from moving any farther."
        $ the_girl.call_dialogue(f"{standing_finger.associated_taboo}_taboo_break")
        $ play_moan_sound()
        "She lets go of your hand, and you slide it down to your prize. She moans softly as you touch her, and shivers when you first touch her clit."
    "After teasing her for a moment you press two fingers between her slit, sliding them into the wet passage beyond her pussy lips."
    return

label scene_standing_finger_1(the_girl, the_location, the_object):
    if the_girl.has_large_tits:
        if the_girl.tits_available:
            "You reach your free hand up to [the_girl.title]'s bare tits and cup one, massaging it while you finger her."

        else:
            "You reach your free hand up to [the_girl.title]'s tits and squeeze one through her clothing, enjoying its size and weight."
    else:
        if the_girl.tits_available:
            "You paw at [the_girl.possessive_title]'s [the_girl.tits_description] with your free hand, running your thumb over one of her nipples as you continue to finger her."
            "Her body responds, the nipple hardening as you play with it."
        else:
            "You paw at [the_girl.possessive_title]'s [the_girl.tits_description] through her clothing with your free hand."
            "You can feel her body respond, her nipple hardening enough that you can feel it through the fabric."
    return


label scene_standing_finger_2(the_girl, the_location, the_object):
    "You slide your fingers in and out of her pussy, stroking the inside of that soft tunnel."
    $ play_moan_sound()
    "Each movement draws moans of pleasure from [the_girl.possessive_title], who presses herself against you."
    if the_girl.arousal_perc > 60:
        if the_girl.vagina_available:
            "Her pussy is dripping wet now, dripping juices down her thighs."
        else:
            $ the_item = the_girl.outfit.get_lower_top_layer
            if the_item:
                "Her pussy is dripping wet now, her juices leaving a faint wet spot on her [the_item.display_name]."
                $ the_item = None
    else:
        if the_girl.vagina_available:
            "She places one of her own hands over yours, encouraging you to speed up."
            the_girl "Just like that... Ah..."
        else:
            $ the_item = the_girl.outfit.get_lower_top_layer
            if the_item:
                $ play_moan_sound()
                "You look over her shoulder and watch as your fingers move under her [the_item.display_name], timed to her soft moans of pleasure."
                $ the_item = None
    return

label outro_standing_finger(the_girl, the_location, the_object):
    "[the_girl.title] can tell you are getting close. She grinds her butt back into your crotch."
    "Feeling her butt on your crotch and [the_girl.title]'s hot, tight pussy squeezing your fingers is enough to push you that little bit farther, past the point of no return."
    "You grasp her tightly with your free hand as you cum, shoving your fingers deep into her cunt and making her gasp in surprise."
    $ ClimaxController.manual_clarity_release(climax_type = "air", person = the_girl)
    the_girl "Did you just... Cum?"
    mc.name "Yeah."
    if report_log.get("girl orgasms", 0) > 0:
        the_girl "That's only fair I suppose."
    else:
        the_girl "Aww, I thought I was going to get there first. Oh well."

    if the_girl.wants_creampie or the_girl.has_cum_fetish:
        the_girl "Maybe next time we'll find somewhere else for you to do that."
        if the_girl.opinion.drinking_cum > 0:
            "[the_girl.title] winks at you as she licks her lips."
        elif the_girl.opinion.cum_facials > 0:
            "[the_girl.title] strokes her cheek and pouts at you."
        elif the_girl.opinion.being_covered_in_cum > 0:
            if the_girl.tits_available:
                "[the_girl.title] strokes her bare chest with her hand."
            else:
                "[the_girl.title] strokes her neck."
        else:
            "[the_girl.title] winks at you and pouts slightly."

    if the_girl.opinion.being_fingered < 0:
        the_girl "Now, you've cum, can we do something else?"
        "[the_girl.title] gently pulls your hand up from her [the_girl.pubes_description] pussy."
        if the_girl.sluttiness >= 60:
            "She brings your hand up to her mouth. She slides your fingers, fresh from her cunt, into her mouth."
            "Her tongue wraps around them as she sucks gently on your fingers. She works her hips, grinding her ass against you."
        else:
            $ play_moan_sound()
            "She moans and works her hips back against you, grinding her ass against you."
    elif the_girl.opinion.being_fingered > 0 or report_log.get("girl orgasms", 0) > 0:
        the_girl "Now, what is it that you were doing?"
        "[the_girl.title] gently holds your arm in place with your hand at her pussy."
        the_girl "You are so good at this."
    else:
        the_girl "Don't forget what you were doing."
    return

label transition_standing_fingering_standing_grope(the_girl, the_location, the_object):
    "You give her a few wet pussy a few more strokes, then pull your fingers out and drag them along her stomach."
    $ play_moan_sound()
    if the_girl.sluttiness >= 60:
        "She moans and takes hold of your hand, bringing it up to her mouth. She slides your fingers, fresh from her cunt, into her mouth."
        "Her tongue wraps around them as she sucks gently on your fingers. She works her hips, grinding your erection against her ass."
    else:
        "She moans and works her hips back against you, grinding your erection against her ass."
    return

label transition_default_standing_finger(the_girl, the_location, the_object):
    $ standing_finger.redraw_scene(the_girl)
    "You gather [the_girl.title] up in your arms, cradling her from behind. You reach a hand between her legs, sliding it down to her pussy."
    "You don't waste any time sliding two fingers into her warm, wet pussy."
    return

label strip_standing_finger(the_girl, the_clothing, the_location, the_object):
    the_girl "Your hands feel amazing... Oh my god..."
    $ the_girl.draw_animated_removal(the_clothing, position = standing_finger.position_tag)
    $ play_moan_sound()
    "She strips off her [the_clothing.name] while you're fingering her, moaning the whole time."
    return

label strip_ask_standing_finger(the_girl, the_clothing, the_location, the_object):
    the_girl "Everything feels so tight, I want to take it all off... Do you mind?"
    "[the_girl.possessive_title!c] grabs onto her [the_clothing.name], waiting for you to tell her what to do."
    menu:
        "Let her strip":
            mc.name "Take it off. Strip for me."
            $ the_girl.draw_animated_removal(the_clothing, position = standing_finger.position_tag)
            "[the_girl.possessive_title!c] takes off her [the_clothing.name] and drops it to the side while you pump your fingers in and out of her cunt."
            return True

        "Leave it on":
            mc.name "No, I like how you look with it on."
            if the_girl.sluttiness < 80:
                the_girl "Do you think I look sexy in it?"
            else:
                the_girl "Don't you think I would look better wearing your cum? That would be so fitting for your dirty little slut, wouldn't it?"
            return False

label orgasm_standing_finger(the_girl, the_location, the_object):
    the_girl "Oh god... Right there! Right there! Ahhhhh!"
    "Her whole body tenses up and she leans back into you. A shiver runs through her body as she climaxes."
    $ the_girl.call_dialogue("climax_responses_foreplay")
    "She quivers with pleasure for a few seconds before her whole body relaxes."
    the_girl "Ah... Keep going..."
    return
