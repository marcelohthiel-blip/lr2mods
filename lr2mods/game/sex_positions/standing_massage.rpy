label intro_standing_grope(the_girl, the_location, the_object):
    $ standing_grope.redraw_scene(the_girl)
    "You stand behind [the_girl.title] and put your arms around her, pulling her close against you."
    "You reach one hand down, running across her stomach and towards her waist and the other up towards her tits."
    if the_girl.has_large_tits:
        "She sighs and leans into you as you cup one of her large tits and heft it up, massaging it gently."
        "Your other hand slides between her legs, brushing against her inner thighs and caressing her pussy."
    else:
        "She sighs and leans into you as your hand slides between her legs, brushing her thighs and petting her pussy."
    return

label taboo_break_standing_grope(the_girl, the_location, the_object):
    "You put your hands on [the_girl.title]'s arms, rubbing them gently."
    the_girl "Oh..."
    "Next, you slide your hands down her body, over the curves of her torso onto her hips."
    "You take a small step forward and slide your hands behind [the_girl.possessive_title] and onto her ass."
    $ the_girl.call_dialogue(f"{standing_grope.associated_taboo}_taboo_break")
    $ standing_grope.redraw_scene(the_girl)
    "You step behind [the_girl.title], putting one arm across her torso and cupping one of her [the_girl.tits_description]."
    "Your other hand slides lower, over her hips again and down to her inner thigh. She sighs happily and leans against you."
    return

label scene_standing_grope_1(the_girl, the_location, the_object):
    if the_girl.has_large_tits:
        if the_girl.tits_available:
            "You squeeze and massage [the_girl.possessive_title]'s bare tits. They're soft, warm, and heavy in your hand."

        else:
            "You squeeze and massage [the_girl.possessive_title]'s [the_girl.tits_description]. They're pleasantly soft and heavy underneath her clothing."
    else:
        if the_girl.tits_available:
            "You paw at [the_girl.possessive_title]'s [the_girl.tits_description], cupping one in your hand and running a thumb over her nipple."
            "Her body responds, her nipple hardening as you play with it."
        else:
            "You paw at [the_girl.possessive_title]'s [the_girl.tits_description] over her clothing."
            "You can feel her body respond, her nipples hardening enough that you can feel them through the fabric."
    return


label scene_standing_grope_2(the_girl, the_location, the_object):
    if the_girl.vagina_available:
        "[the_girl.title] spreads her legs for you, giving you space between them to slide your hand down."
        $ play_moan_sound()
        "She moans softly when you run a finger over her warm, wet, slit."
        the_girl "Oh... Don't tease me like that..."
    else:
        "[the_girl.title] spreads her legs for you, and you rub her crotch through her [the_girl.outfit.get_lower_top_layer.display_name]."
        $ play_moan_sound()
        the_girl "Mmm..."
    return

label scene_standing_grope_3(the_girl, the_location, the_object):
    if the_girl.vagina_available:
        "[the_girl.title] presses her hips back against you, grinding her bare ass rubbing against your crotch."
        if not mc.recently_orgasmed:
            the_girl "Mmm, I can feel your erection. That's so fucking hot..."
    else:
        "[the_girl.title] presses her hips back against you, grinding her ass against your crotch."
    return

label outro_standing_grope(the_girl, the_location, the_object):
    if the_girl.arousal_perc >= 100:
        "Hearing [the_girl.title] cum in your arms pushes you over the edge. You feel your cock spasm in your underwear as a wave of pleasure washes over you."
        $ climax_controller = ClimaxController(["Cum in your pants.", "air"])
        $ the_choice = climax_controller.show_climax_menu()
        $ climax_controller.do_clarity_release(the_girl)
        "It takes both of you a moment to recover from your orgasms."
    else:
        "Feeling [the_girl.title]'s body under your hands is enough to push you that little bit farther, past the point of no return."
        $ climax_controller = ClimaxController(["Cum in your pants.", "air"])
        $ the_choice = climax_controller.show_climax_menu()
        $ climax_controller.do_clarity_release(the_girl)
        "You grasp her tightly as you cum, pumping your load out into your pants."
        the_girl "Did you just... Cum?"
        mc.name "Yeah."
        "She grinds her butt back into your crotch."
        the_girl "Mmm, good to hear."
    return

# label transition_standing_grope_blowjob(the_girl, the_location, the_object):
#
#     return

#TODO: Add a "finger" position that is reachable from here.

label transition_standing_grope_standing_fingering(the_girl, the_location, the_object):
    if the_girl.vagina_available:
        "You pet [the_girl.title]'s pussy, then slide two fingers inside it. She gasps as they slip inside."
    else:
        $ the_item = the_girl.outfit.get_lower_top_layer
        if the_item:
            "You slide a hand under her [the_item.display_name], bringing your hand right to her pussy."
            "She gasps as you tease it with two fingers, then slip them inside the wet hole."
            $ the_item = None
        else:
            "You pet [the_girl.title]'s pussy, then slide two fingers inside it. She gasps as they slip inside."
    the_girl "Oh [the_girl.mc_title]... Ah..."
    return

label transition_default_standing_grope(the_girl, the_location, the_object):
    $ standing_grope.redraw_scene(the_girl)
    "You gather [the_girl.title] up in your arms, cradling her from behind. You reach one hand down between her legs, and the other up to cup her breasts."
    "She leans her weight against you in response."
    return

label strip_standing_grope(the_girl, the_clothing, the_location, the_object):
    the_girl "Your hands feel amazing... Oh my god..."
    $ the_girl.draw_animated_removal(the_clothing, position = standing_grope.position_tag)
    "She strips off her [the_clothing.name] while you're feeling her up."
    return

label strip_ask_standing_grope(the_girl, the_clothing, the_location, the_object):
    the_girl "I want to feel you touch me everywhere... Can I take off my [the_clothing.name] for you?"
    menu:
        "Let her strip":
            mc.name "Take it off for me."
            $ the_girl.draw_animated_removal(the_clothing, position = standing_grope.position_tag)
            "You watch while [the_girl.possessive_title] takes off her [the_clothing.name] and drops it to the side."
            return True

        "Leave it on":
            mc.name "No, I like how you look with it on."
            if the_girl.sluttiness < 80:
                the_girl "Do you think I look sexy in it?"
            else:
                the_girl "Don't you think I would look better wearing your cum? That would be so fitting for your dirty little slut, wouldn't it?"
            return False

label orgasm_standing_grope(the_girl, the_location, the_object):
    "You feel [the_girl.possessive_title] tense up in your arms as you explore her body."
    $ the_girl.call_dialogue("climax_responses_foreplay")
    "She quivers with pleasure for a few seconds before her whole body relaxes."
    the_girl "Ah... Keep going... I might be able to cum again!"
    return
