#Anytime game quest. Available immediately, requires lab leader and at least 1 person in marketing. Gains potency as the game progresses.
#MC and science leader discover cure for some disease.
#At low tier research, discover cure for some obscure disease
#Higher tiers, discover cure for high profile diseases.
#MC realises he is not equipped to handle production of the drug. Work with marketing to sell patent rights.
#Patent rights sold gives extra cash based on what disease was cured. Should be significant but not OP sum.

# flags
# 1: Quest has been initiated
# 9: If at any point during the quest we fire the head researched. (BAD END)
# 11: Science leader contacts MC, informs him that she stumbled on cure for disease. X
# 18: MC decides to try and keep them all for himself to manufacture small scale.
# 19: MC discovers girl secretly donated drug to large pharma company. (BAD END)
# 21: MC decides to try and sell patent for the drug. Decides to work with someone from marketing.
# 29: After 7 days, if MC hasn't initiated sale with marketing, MC discovers large PHARMA company just announced cure. His cure is worthless (BAD END)
# 31: Talked to marketing about trying to sell patent rights.
# 101: A few days later, marketing returns and gives MC details on selling the patent rights. Payout!

init 1 python:
    def setup_quest_cure_discovery():
        quest = quest_cure_discovery()
        quest.quest_event_dict["start_day"] = 9999
        quest.quest_event_dict["cure_tier"] = mc.business.research_tier  #Snapshot quest tier at the beginning.
        quest.quest_event_dict["disease_name"]  = quest_cure_discovery_disease_name()
        contact = get_random_from_list(mc.business.market_team)
        quest.quest_event_dict["market_contact"] = contact.identifier
        quest.quest_event_dict["market_day"] = 9999
        #TODO add start event
        quest.set_quest_flag(1)
        mc.business.add_mandatory_crisis(quest_cure_discovery_intro)
        game_hints.append(Hint("Medical Breakthrough", "Your head researcher has some exciting news.", "quest_cure_discovery().get_quest_flag() <= 1", "quest_cure_discovery().get_quest_flag() > 1"))
        hint_string = "Talk to " + contact.title + " about selling your medical patent."
        game_hints.append(Hint("Medical Breakthrough", hint_string, "quest_cure_discovery().get_quest_flag() == 21", "quest_cure_discovery().get_quest_flag() != 21"))
        game_hints.append(Hint("Medical Breakthrough", "Wait for your patent rights to sell", "quest_cure_discovery().get_quest_flag() == 31", "quest_cure_discovery().get_quest_flag() != 31"))

        return

    def quest_cure_discovery():
        return quest_director.get_quest("Medical Breakthrough")

    def quest_cure_get_market_contact():
        contact = quest_cure_discovery().quest_event_dict.get("market_contact", None)
        if isinstance(contact, str):
            return Person.get_person_by_identifier(contact)
        return contact

    def quest_cure_set_market_contact(person):
        quest_cure_discovery().quest_event_dict["market_contact"] = person.identifier



#Quest defining functions
    def quest_cure_discovery_tracker():
        quest = quest_cure_discovery()
        if quest.get_quest_flag() <= 101 and quest.get_quest_flag() > 1:
            if quest_cure_get_market_contact() is None: #We lose our marketing contact
                quest.quest_completed()
        return

    def quest_cure_discovery_start_requirement():
        if day < 30:
            return False    # don't start too soon
        if mc.business.research_tier < 1:
            return False    # we should have made some research progress
        if mc.business.head_researcher is None:
            return False    # we need a head researcher
        if builtins.len(mc.business.market_team) == 0:
            return False    # we need someone in marketing
        return True

    def quest_cure_discovery_cleanup():
        mc.business.remove_mandatory_crisis("quest_cure_discovery_intro_label")
        mc.business.remove_mandatory_crisis("quest_cure_discovery_market_patent_label")
        mc.business.remove_mandatory_crisis("quest_cure_discovery_patent_sold_label")
        mc.business.remove_mandatory_crisis("quest_cure_discovery_patent_kept_label")
        mc.business.remove_mandatory_crisis("quest_cure_discovery_market_missed_label")
        person = quest_cure_get_market_contact()
        if person:
            person.remove_on_talk_event(quest_cure_discovery_market_patent)
        quest_cure_discovery().quest_event_dict.clear()
        return



#Label requirement functions
    def quest_cure_discovery_intro_requirement():
        if mc.business.is_open_for_business: #Only trigger if people are in the office.
            return True
        return False

    def quest_cure_discovery_market_patent_requirement(the_person):
        if mc.business.is_open_for_business:
            if the_person.is_at_work:
                return True #Only while she is at work
        return False

    def quest_cure_discovery_patent_sold_requirement():
        if day >= quest_cure_discovery().quest_event_dict.get("market_day", 0) + 3:
            if mc.business.is_open_for_business:
                return True
        return False

    def quest_cure_discovery_patent_kept_requirement():
        if day >= (quest_cure_discovery().quest_event_dict.get("start_day", 9999) + 1):
            if mc.business.is_open_for_business and mc.is_at_office: # only during office hours and we are at work (dialog depends on it)
                return True
        return False

    def quest_cure_discovery_market_missed_requirement():
        if day >= (quest_cure_discovery().quest_event_dict.get("start_day", 9999) + 7) : #One week to talk to marketing
            if time_of_day == 4:
                return True
        return False


    #Action declarations

    quest_cure_discovery_intro = Action("Begin Cure Discovery Quest", quest_cure_discovery_intro_requirement, "quest_cure_discovery_intro_label")
    quest_cure_discovery_market_patent = Action("Attempt to sell patent", quest_cure_discovery_market_patent_requirement, "quest_cure_discovery_market_patent_label")
    quest_cure_discovery_patent_sold = Action("Patent Sold", quest_cure_discovery_patent_sold_requirement, "quest_cure_discovery_patent_sold_label")
    quest_cure_discovery_patent_kept = Action("Patent Stolen", quest_cure_discovery_patent_kept_requirement, "quest_cure_discovery_patent_kept_label")
    quest_cure_discovery_market_missed = Action("Patent Worthless", quest_cure_discovery_market_missed_requirement, "quest_cure_discovery_market_missed_label")

#Quest Labels
label quest_cure_discovery_init_label():
    $ setup_quest_cure_discovery()
    return

label quest_cure_discovery_intro_label():
    $ the_person = mc.business.head_researcher
    $ the_disease = quest_cure_discovery().quest_event_dict.get("disease_name", "Rabies")
    $ quest_cure_discovery().quest_event_dict["start_day"] = day
    if the_person is None:
        return #Bad end
    if not mc.is_at(rd_division):
        $ mc.start_text_convo(the_person)
        the_person "Hey, I need to see you in the lab ASAP!"
        mc.name "On my way!"
        $ mc.end_text_convo()
        "You quickly head to the lab."
        $ mc.change_location(rd_division)

    $ the_person.draw_person()
    the_person "Hey! I need to talk to you about something."
    mc.name "What is it?"
    the_person "I was doing some research for a new serum trait. I was testing the effects on some rats in the lab, when I noticed something incredible."
    the_person "I had some rats that have [the_disease]. After giving them the serum, they showed no signs of the disease in less than 12 hours!"
    mc.name "That's... interesting?"
    the_person "I thought so too, so I ran a bunch more tests. In 99.8 percent of tests I ran, the disease appears to be completely cured!"
    mc.name "Is that something that could be adapted to humans?"
    the_person "This would be the first step in researching a cure! I already filed a patent for the business on the formula."
    mc.name "That's great... but could we realistically manufacture that here?"
    "She considers your question for a moment."
    the_person "Realistically... not really. Not in the numbers that would be needed to have it available to large numbers of people."
    mc.name "Hmm, that's a shame."
    the_person "Maybe you could... I don't know... sell the patent? To a larger pharmaceutical company? One that could make it in the quantity needed to meet worldwide demand?"
    mc.name "That's an interesting idea..."
    menu:
        "Keep the secret for your company":
            mc.name "It might take us some time, but I think we could ramp up production here to meet demand."
            $ the_person.draw_person(emotion = "angry")
            the_person "But sir! That would take... months? Or more?"
            mc.name "So?"
            the_person "Think of all the people out there, suffering right now. Surely it would be better for us to just sell the rights?"
            mc.name "No, I don't think so."
            $ the_person.change_stats(obedience = -10, love = -10, happiness = -20)
            "She really doesn't like your answer. Hopefully you haven't burned any bridges with your answer?"
            "As you turn to leave, you can hear her muttering something."
            $ del the_disease
            $ quest_cure_discovery().set_quest_flag(18)
            $ mc.business.add_mandatory_crisis(quest_cure_discovery_patent_kept)
            return
        "Try and sell the patent":
            mc.name "Some cash infusion to the company would be great. That's a great idea, [the_person.title]."
    $ the_target = quest_cure_get_market_contact()
    the_person "Personally, I think you should talk to [the_target.name]. You know, over in marketing?"
    mc.name "Oh?"
    if the_target == alexia:
        the_person "She's a recent college graduate and seems to have a good handle on things over there. I bet she could manage it!"
        mc.name "Noted. I'm not sure I'll have time, but I'll talk to her when I can."
    elif the_target == lily or the_person == mom or the_person == cousin or the_person == aunt:
        the_person "Corporate espionage is huge, and a discovery like this could make big waves."
        the_person "You should probably ask someone you can trust to handle this, like someone from your family."
        mc.name "Good idea. I'll talk to her as soon as I can."
    else:
        the_person "Yeah, I think she actually has experience doing something similar at a previous job. I bet she could help out!"
        mc.name "Noted. I'm not sure I'll have the time, but I'll talk to her when I can."
    the_person "If I were you, I'd get on it, quick! Modern day drug research is extremely fast-paced. No telling when another lab might replicate our findings..."
    mc.name "Thank you, [the_person.title], for your research and for bringing this to my attention."
    "So... you should talk to [the_target.possessive_title] about selling your patent rights to the cure for [the_disease]."

    python:
        quest_cure_set_market_contact(the_target)
        quest_cure_discovery().set_quest_flag(21)
        the_target.add_unique_on_talk_event(quest_cure_discovery_market_patent)
        mc.business.add_mandatory_crisis(quest_cure_discovery_market_missed)
        del the_disease
        del the_target
    return

label quest_cure_discovery_market_patent_label(the_person):
    $ the_person.draw_person()
    $ the_disease = quest_cure_discovery().quest_event_dict.get("disease_name", "Rabies")
    mc.name "Hello [the_person.title], do you have a moment?"
    the_person "Of course. What can I do for you sir?"
    if the_person == alexia:
        mc.name "We made a big discovery in the research lab, but it is too big for our production department to handle. I was wondering if you could look into selling some patent rights."
        the_person "Oh? I think I could handle something like that. What is the patent for?"
        mc.name "Our research department made a discovery related to a possible treatment for [the_disease]."
    elif the_person == lily or the_person == mom or the_person == cousin or the_person == aunt:
        mc.name "We made a big discovery in the research lab, but it is too big for our production department to handle. I was wondering if you could look into selling some patent rights."
        the_person "Oh? Why... why would you ask me to do that?"
        mc.name "If word gets out that we made this discovery, we might be the target of some bad actors. I need someone I can trust to handle this. Someone from the family."
        the_person "Okay, what is the discovery?"
        mc.name "Our research department made a discovery related to a possible treatment for [the_disease]."
    else:
        mc.name "Well, I heard that you might have some prior experience working with drug patent rights..."
        the_person "Yes sir! At my last job, I worked for a pharmaceutical investment company, buying and selling patent rights to all kinds of different drugs."
        mc.name "Wow, well, that is actually very useful. You see, our research department made a discovery related to a possible treatment for [the_disease]."
    the_person "Oh wow! There's currently some preventative drugs for that, but no known cure."
    mc.name "I know. I wish we had the production and testing capabilities here to take it to market, but unfortunately, we just don't."
    the_person "Aahhh, I see. So you want me to test the waters and see what I can get for the patent rights to the discovery?"
    mc.name "That's exactly right."
    the_person "Okay! I can do that. Give me a couple of days and I'll see what I can find!"
    mc.name "Thank you, [the_person.title]."
    $ quest_cure_set_market_contact(the_person)
    $ quest_cure_discovery().quest_event_dict["market_day"] = day
    $ del the_disease
    $ quest_cure_discovery().set_quest_flag(31)
    $ mc.business.remove_mandatory_crisis("quest_cure_discovery_market_missed_label")
    $ mc.business.add_mandatory_crisis(quest_cure_discovery_patent_sold)
    return

label quest_cure_discovery_patent_sold_label():
    $ the_disease = quest_cure_discovery().quest_event_dict.get("disease_name", "Rabies")
    $ the_person = quest_cure_get_market_contact()
    if the_person is None:
        return
    #TODO test to make sure market contact still works for us.
    $ mc.start_text_convo(the_person)
    the_person "Hey there! I just got some good news on that patent you have for [the_disease]."
    mc.name "Glad to hear it. What is the news?"
    if quest_cure_discovery().quest_event_dict.get("cure_tier", 0) == 0:
        the_person "Well, [the_disease] has very few cases annually, so the prospects of a lucrative deal for the patent rights were pretty slim."
        the_person "After negotiating, I was able to sell them for $1500. I hope that is okay."
        $ mc.business.change_funds(1500)
        mc.name "I understand. That is still very helpful. Thank you [the_person.title]."
    elif quest_cure_discovery().quest_event_dict.get("cure_tier", 0) == 1:
        the_person "Well, [the_disease] really only propagates in poor, tropical areas, due to the way it spreads."
        the_person "While the good this drug can do is great, the profit potential is pretty low. I was only able to sell it for $3500. I hope that is okay."
        $ mc.business.change_funds(3500)
        mc.name "Thank you [the_person.title], I just hope the drug can be put to good use."
    elif quest_cure_discovery().quest_event_dict.get("cure_tier", 0) == 2:
        the_person "[the_disease] is widespread in the developed world. However, because this treatment has only been shown effective in rats, the over all effectiveness is unknown."
        the_person "After negotiating, I was able to sell the patent for $15000. I hope that is okay."
        $ mc.business.change_funds(15000)
        mc.name "That is still a considerable sum. Thank you [the_person.title]."
    elif quest_cure_discovery().quest_event_dict.get("cure_tier", 0) >= 3:
        the_person "[the_disease] is widespread in older populations. However, because this treatment has only been shown effective in rats, the over all effectiveness is unknown."
        the_person "After negotiating, I was able to sell the patent for $50000. I hope that is okay."
        $ mc.business.change_funds(50000)
        mc.name "That is still a significant sum. Thank you [the_person.title]."
    $ mc.end_text_convo()
    "The patent is sold! And you made a little extra money for the business."
    $ del the_disease
    $ quest_cure_discovery().set_quest_flag(101)
    $ quest_cure_discovery().quest_completed()
    return

#Bad Ends and paths

label quest_cure_discovery_patent_kept_label():
    $ the_person = mc.business.head_researcher
    $ the_disease = quest_cure_discovery().quest_event_dict.get("disease_name", "Rabies")
    $ quest_cure_discovery().set_quest_flag(19)
    "You get a notification on your phone and you check it. It's from the Red Cross?"
    "Red Cross""Thank you for donating your patent for [the_disease]!"
    "Red Cross""With this donation, we promise we will work to the best of our abilities to get this cure into the hands of everyone who needs it, worldwide."
    "Donated? What the hell? You didn't donate that!"
    if the_person is None:
        "Suddenly, you realise what must have happened. After clearing out her desk, the old head researcher must have donated the patent she discovered!"
        "Well, maybe you should have considered selling the patent. Either way, that business opportunity is now gone."
        $del the_disease
        $ quest_cure_discovery().quest_completed()
        return
    else:
        "Suddenly, you realise what must have happened. [the_person.title], not happy with your intention to keep the patent, must have secretly donated the rights to it."
    "You call her up."
    the_person "Hello?"
    mc.name "Hey. So, I'm guessing you're the one I have to thank for the email I got this morning from the Red Cross?"
    "There is silence on the other end. You think you hear an expletive whispered."
    the_person "I'm not going to lie... yes, that was me. You have to understand! This could help a lot of people!"
    "She sounds very sincere. It's hard to be mad, and maybe this is something that really {i}could{/i} help a lot of people."
    the_person "Please don't fire me, I love working here, but I just couldn't sit by while something that could help people..."
    menu:
        "Fire her":
            mc.name "Clean out your desk. I can't have someone undermining me as my head researcher. You're fired."
            "Stunned silence on the other end of the call. Finally she speaks up."
            the_person "I understand. I'll be out before the end of the day."
            $ mc.business.remove_employee(the_person)
        "It's okay":
            mc.name "It's okay, [the_person.title]. I'm sorry I didn't make that move right from the start. You did the right thing."
            "She sounds relieved."
            the_person "Oh [the_person.mc_title], I knew you were a reasonable man! I'll make it up to you, I promise!"
            if the_person.effective_sluttiness() > 50: #MC can push for make up sex.
                mc.name "I think I know how you can make it up to me. Come to my office."
                the_person "Yes sir!"
                $ ceo_office.show_background()
                $ the_person.draw_person()
                "You hear a knock. You look up and see [the_person.possessive_title]."
                the_person "You wanted to see me?"
                mc.name "Yes. Come in, and lock the door behind you."
                the_person "Oh my..."
                "[the_person.title] does as you ask. Her voice takes on a sultry tone."
                the_person "So, did you have something in mind? How can I make all of this up to you?"
                mc.name "I do, come around here and get on your knees."
                the_person "Oh god, yes [the_person.mc_title]."
                $ the_person.draw_person(position = "blowjob")
                "You pull your dick out of your pants and put it right on her face."
                mc.name "You know what to do."
                "You could let her just give you a blowjob, but if you push things a little rougher, it would really drive the point home, but her admiration for you would probably decrease."
                "[the_person.title] opens her mouth and takes the tip of your cock in her hot mouth. She gives you a few strokes as you rapidly harden in her mouth."
                call fuck_person(the_person, start_position = blowjob, start_object = mc.location.get_object_with_trait("Kneel"), skip_intro = True, position_locked = True, ignore_taboo = True) from _quest_cure_sex_path_01
                $ the_report = _return
                if skull_fuck in the_report.get("positions_used", []):  #You really roughed her up
                    "[the_person.possessive_title!c] mascara is running from tears caused by being gagged when you roughly fucked her throat."
                    mc.name "I know that this story had a happy ending, with the patent going to the Red Cross, but remember, this is my business. Don't do things behind my back again."
                    $ the_person.change_stats(obedience = 10, love = -10, slut = 3, max_slut = 70)
                    "Her voice is trembling as she responds."
                    the_person "Yes... yes sir..."
                elif deepthroat in the_report.get("positions_used", []):  #She took it deep.
                    "[the_person.possessive_title!c] is recovering from taking your cock deep down her throat."
                    mc.name "I know this story had a happy ending, with the patent going to the Red Cross, but please don't do things behind my back like that again."
                    $ the_person.change_stats(obedience = 5, slut = 2, max_slut = 70)
                    the_person "Yes sir, it won't happen again!"
                else: #Just a BJ
                    "[the_person.possessive_title!c] licks her lips, she seems to have enjoyed getting on her knees for you."
                    mc.name "Thank you for doing the right thing, but please let me know before you take actions like that again."
                    $ the_person.change_stats(love = 5, slut = 1, max_slut = 70)
                    the_person "Yes sir."
                mc.name "That'll be all for now."
                $ the_person.draw_person(position = "walking_away")
                "Well, you may have missed a financial opportunity, but at least you got a blowjob out of it!"
            else:
                mc.name "I'm sure you will. Please try to let me know before you take actions like that in the future though."
                $ the_person.change_stats(obedience = 5, love = 5, happiness = 10) #Net gain to stats because MC still took a good path in the end.
                the_person "Of course sir."
                "You say goodbye and hang up the phone."
                "Well, you may have missed out on a financial opportunity, but it sounds like you've gained some consideration from [the_person.title] in the process."
    $ del the_disease
    $ quest_cure_discovery().quest_completed()
    return

label quest_cure_discovery_market_missed_label():
    $ the_disease = quest_cure_discovery().quest_event_dict.get("disease_name", "Rabies")
    "Before heading for bed, you check on the latest news on your computer. A headline catches your attention."
    "NEWS""BREAKING NEWS: Scientists at BIOFIRM announce possible cure for [the_disease]."
    "Wait a minute... isn't that the disease your head researcher recently found a possible cure for?"
    "FUCK, she was right. You probably should have moved on those patent rights faster!"
    #TODO remove on talk event to market target.
    $ del the_disease
    $ quest_cure_discovery().set_quest_flag(29)
    $ quest_cure_discovery().quest_completed()
    return
