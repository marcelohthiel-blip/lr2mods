#### EMPLOYEE ACTION LABELS ####

label employee_set_duties_label(the_person):
    mc.name "[the_person.title], let's talk about what you do around here..."
    call set_duties_controller(the_person, the_person.primary_job) from _call_set_duties_controller_set_duties
    if _return:
        $ the_person.set_event_day("work_duties_last_set")
    return

label initial_set_duties_label(the_person):
    if len(the_person.duties) >= the_person.primary_job.seniority_level:
        return # quick exit all duties filled

    mc.name "Let's talk about your duties as [the_person.primary_job.job_definition.job_title]."
    call set_duties_controller(the_person, the_person.primary_job) from _call_set_duties_controller_initial_set_duties
    if _return:
        $ the_person.set_event_day("work_duties_last_set")
        mc.name "Do you think you can handle all that?"
        "[the_person.possessive_title!c] nods."
        the_person "Yes [the_person.mc_title], I can."
    else:
        mc.name "Actually, let's discuss this another time, okay?"
        "[the_person.possessive_title!c] nods."
    return

label employee_complement_work(the_person):
    $ the_person.set_event_day("day_last_employee_interaction")
    if mc.business.employee_count == 1:
        mc.name "[the_person.title], I wanted to tell you that you've been doing a great job lately. Me and you make a great team, and I couldn't do all of this without you."
    else:
        mc.name "[the_person.title], I wanted to tell you that you've been doing a great job lately. Keep it up, you're one of the most important players in this whole operation."

    $ the_person.change_stats(happiness = mc.charisma, love = 1)
    the_person "Thanks [the_person.mc_title], it means a lot to hear that from you!"
    $ mc.stats.change_tracked_stat("Employee", "Complemented Work", 1)
    return

label insult_recent_work(the_person):
    $ the_person.set_event_day("day_last_employee_interaction")
    if mc.business.employee_count == 1:
        mc.name "I'm not sure what's going on with you lately, but I'm going to need you to try a little harder. It's only me and you here and you're really letting me down."
    else:
        mc.name "Honestly [the_person.title], I've been disappointed with your work lately and I really need you to try a little harder. You're letting the whole team down."
    if the_person.obedience >= 120:
        "She seems shocked for a second, then nods."
        the_person "I'm sorry. I'll try harder."
    else:
        the_person "What? I... I've been doing my best."
        mc.name "Well I'll need your best to be a little better if you want to justify what I'm paying you."
        $ the_person.draw_person(position = "sitting", emotion = "sad")
        "She scowls, but nods and doesn't object any more."
    $ the_person.change_stats(happiness = -5, obedience = mc.charisma, love = -2)
    $ mc.stats.change_tracked_stat("Employee", "Insulted Work", 1)
    return

label employee_pay_cash_bonus(the_person):
    $ the_person.set_event_day("day_last_employee_interaction")
    mc.name "[the_person.title], I noticed you've been putting in a lot of good work at the lab lately. I wanted to give you a little extra to make sure you know you're appreciated."
    "You pull out your wallet and start to pull out a few bills."
    $ days_wages = the_person.current_job.salary
    if days_wages < 5:
        $ days_wages = 5
    $ weeks_wages = days_wages * 5
    $ months_wages = days_wages * 20
    menu:
        "Give her a pat on the back":
            mc.name "And I'll absolutely do that once the next batch of sales go through."
            $ the_person.draw_person(emotion = "sad")
            if 5 - mc.charisma > 0:
                $ the_person.change_happiness(5 - mc.charisma)
            "[the_person.title] looks visibly disappointed."
            the_person "Right, of course."

        "Give her a day's wages\n{menu_red}Costs: $[days_wages:.2f]{/menu_red}" if mc.business.has_funds(days_wages):
            mc.name "Here you go, treat yourself to something nice tonight."
            $ the_person.draw_person(emotion = "happy")
            $ mc.business.change_funds(-days_wages, stat = "Salaries")
            $ the_person.change_happiness(1 + mc.charisma)
            "[the_person.title] takes the bills from you and smiles."
            the_person "Thank you sir."


        "Give her a week's wages\n{menu_red}Costs: $[weeks_wages:.2f]{/menu_red}" if mc.business.has_funds(weeks_wages):
            mc.name "Here you go, don't spend it all in once place."
            $ the_person.draw_person(emotion = "happy")
            $ the_person.change_stats(happiness = 3 + mc.charisma, obedience = mc.charisma)
            $ mc.business.change_funds(-weeks_wages, stat = "Salaries")
            "[the_person.title] takes the bills, then smiles broadly at you."
            the_person "That's very generous of you sir, thank you."

        "Give her a month's wages\n{menu_red}Costs: $[months_wages:.2f]{/menu_red}" if mc.business.has_funds(months_wages):
            mc.name "Here, you're a key part of the team and you deserved to be rewarded as such."
            $ the_person.draw_person(emotion = "happy")
            $ the_person.change_stats(happiness = 10 + mc.charisma, obedience = 5 + mc.charisma)
            $ mc.business.change_funds(-months_wages, stat = "Salaries")
            "[the_person.title] takes the bills, momentarily stunned by the amount."
            if the_person.effective_sluttiness() > 40 and the_person.happiness > 100:
                the_person "Wow... this is amazing sir. I'm sure there's something I can do to pay you back, right?"
                $ mc.change_locked_clarity(5)
                "She steps close to you and runs a finger down your chest."
                $ the_person.add_situational_slut("situation", 10, "He's given me such a generous bonus, I should repay the favour!")
                call fuck_person(the_person) from _call_fuck_person_3
                #Now that you've had sex, we calculate the change to her stats and move on.
                $ the_person.call_dialogue("sex_review", the_report = _return)
                $ the_person.clear_situational_slut("situation")
                $ the_person.review_outfit()
            else:
                the_person "Wow... this is amazing sir. I'll do everything I can for you and the company!"

    $ mc.stats.change_tracked_stat("Employee", "Cash Bonusses", 1)
    return

label employee_performance_review(the_person):
    $ the_person.set_event_day("day_last_performance_review")
    #Bring them into the office. (Set the background appropriately)
    mc.name "[the_person.title], I'd like to have a talk with you about your recent performance here at [mc.business.name]. Can we talk in my office for a moment?"
    if the_person.obedience > 150:
        the_person "Oh, of course sir."
    else:
        the_person "Uh, I guess so."

    $ mc.change_location(ceo_office)

    "You lead [the_person.title] into your office and close the door behind her. You take your seat at your desk and motion to a chair opposite you."
    $ the_person.draw_person(position = "sitting")
    mc.name "So [the_person.title], tell me what you think about your job."

    if the_person.current_job.job_happiness_score > 0:
        #She's happy enough with the job to stay here
        if the_person.current_job.salary > the_person.current_job.base_salary + 15: #She's very overpaid
            the_person "It's a fantastic position and I'm lucky to have it! There aren't very many places that would be able to pay me as well as I am here."
        elif the_person.current_job.salary > the_person.current_job.base_salary + 3: #She's reasonably over paid.
            the_person "It's a great job. The pay is great, and the work is interesting."
        elif the_person.current_job.salary > the_person.current_job.base_salary - 3: #She's reasonably paid.
            the_person "I really like my job. I feel like I can come in every day and do an honest day's work."
        else:
            the_person "The pay isn't the greatest, but the work environment really makes up for it. It's a joy to be working here."

    else: #She's thinking about quitting.
        if the_person.current_job.salary > the_person.current_job.base_salary + 15: #She's very overpaid
            the_person "The pay is amazing, but the work environment here is just terrible. I honestly don't know how much longer I can take it."
        elif the_person.current_job.salary > the_person.current_job.base_salary + 3: #She's reasonably over paid.
            the_person "I know you're paying me very well, but the work here is terrible. I hope you have some plans to make things better."
        elif the_person.current_job.salary > the_person.current_job.base_salary - 3: #She's reasonably paid.
            the_person "Things could be better. I'd like it if the conditions here at work were improved a little bit, or I could be paid a little bit more."
        else:
            the_person "I don't really have anything positive to say. The pay isn't great and it isn't exactly the most pleasant work environment."

    "You nod and take some notes while you think of how you want to respond."
    #TODO: Here is where characters, especially those with moderate sluttiness and who are over paid, might try and win your favour. Is this the right place for it?
    menu:
        "Reward her for work well done":
            $ ran_num = the_person.current_job.seniority_level + 1

            menu:
                "Offer her kind words": #Raise happiness and obedience a little.
                    call employee_complement_work(the_person) from _call_employee_complement_work

                "Offer her a raise":
                    call employee_salary_improvement_options(the_person) from _call_employee_salary_improvement_options_performance_review_raise

                "Promote her\n{menu_green}+1 Work Experience (To level [ran_num]){/menu_green}" if the_person.current_job.seniority_level < 5:
                    mc.name "Well, [the_person.title], you've become a valuable employee for the company."
                    the_person "Thank you, [the_person.mc_title]. It's great to hear that."
                    mc.name "How would you feel about a promotion?"
                    the_person "I... wow... that would be absolutely amazing. "

                    python:
                        the_person.set_event_day("last_promotion_day")
                        employee_increase_job_experience(the_person)
                        the_person.change_stats(happiness = 10, obedience = 2, love = 2)

                    the_person "I don't know what to say."

                    "You are contemplating if you should give her a raise to go along with the promotion..."
                    menu:
                        "Give her a raise":
                            call employee_salary_improvement_options(the_person) from _call_employee_salary_improvement_options_performance_review_promotion

                        "Don't give her a raise":
                            mc.name "That's okay, I just wanted you to know that you're appreciated."

                    mc.name "Good. Let's talk about your new duties then..."
                    call set_duties_controller(the_person, the_person.primary_job) from _call_set_duties_controller_employee_performance_review
                    if _return:
                        mc.name "I trust you can handle all of that?"
                        "[the_person.possessive_title!c] nods."

                "Reward her sexually" if the_person.effective_sluttiness() >= 40: #At high sluttiness you can make her cum to make her even happier with her job.
                    mc.name "You do a lot of work for the company, and I know how stressful your job can be at times."
                    "You get up from your desk and move around to the other side. You step behind [the_person.title] and place your hands on her shoulders, rubbing them gently."
                    mc.name "I'd like to do something for you to help you relax. How does that sound for a bonus?"
                    $ the_person.add_situational_slut("seduction_approach", 10, "It's all about me!")
                    $ the_person.add_situational_obedience("seduction_approach", -10, "It's all about me!")
                    the_person "Oh [the_person.mc_title], that sounds like a great idea..."
                    call fuck_person(the_person,private = True) from _call_fuck_person_11
                    $ the_report = _return
                    $ the_person.clear_situational_slut("seduction_approach")
                    $ the_person.clear_situational_obedience("seduction_approach")
                    $ the_person.draw_person()
                    if the_report.get("girl orgasms", 0) > 0: #We made her cum! Congratulations!
                        $ the_person.change_stats(happiness = 10, love = 2, slut = 2, max_slut = 80)
                        the_person "Oh [the_person.mc_title], that was wonderful! I couldn't have asked for a better performance bonus!"
                    elif the_report.get("guy orgasms", 0) > 0: # You "rewarded" her by cumming and leaving her unsatisfied. Not particularly impressive.
                        $ the_person.change_stats(happiness = -5, obedience = -2)
                        the_person "It's not much of a bonus if you're the only one who gets to cum. Cash would be better next time."
                    else: #She didn't cum, but neither did you so maybe you were just both tired
                        $ the_person.change_stats(happiness = 3, slut = 1, max_slut = 50)
                        the_person "Well, that was a good time [the_person.mc_title]. It's a lot more fun than a normal performance bonus, that's for sure!"

                    $ the_person.review_outfit()

        "Punish her for poor performance":
            $ cut_amount = builtins.max(the_person.current_job.salary * 0.1, 0) # Cant cut when salary is already $0
            menu:
                "Chastise her": #Lower happiness and love a little, large obedience boost.
                    call insult_recent_work(the_person) from _call_insult_recent_work

                "Cut her pay\n{menu_green}Saves: $[cut_amount:.2f] / day{/menu_green}": #Pay her less. Large happiness and obedience drop.
                    mc.name "I'm really sorry to do this [the_person.title], but your performance lately just doesn't justify what I'm paying you."
                    mc.name "I'm going to have to cut your pay by 10%%."
                    $ the_person.current_job.salary -= cut_amount
                    $ the_person.change_stats(happiness = -(15 - mc.charisma), obedience = -(8 - mc.charisma))
                    if the_person.current_job.job_happiness_score > 0:
                        $ the_person.draw_person(position = "sitting", emotion = "sad")
                        the_person "I... I understand."
                    elif the_person.current_job.job_happiness_score > -25:
                        $ the_person.draw_person(position = "sitting", emotion = "angry")
                        the_person "What? I... I don't know what to say!"
                        mc.name "Like I said, I'm sorry but it has to be done."
                    else: #She's so unhappy with her job she quits.
                        $ the_person.draw_person(position = "sitting", emotion = "angry")
                        the_person "What? I... I can't believe that [the_person.mc_title], why would you ever think I would stay here for less money?"
                        mc.name "Like I said, I'm sorry but it has to be done."
                        the_person "Well you know what, I think I'm just going to find somewhere else to work. I quit."
                        $ clear_scene()
                        "[the_person.title] stands up and storms out."
                        $ mc.business.remove_employee(the_person)
                        call advance_time() from _call_advance_time_12
                        return #Don't use the normal "show her out" ending stuff. The scene ends here.

                "Threaten to fire her": #She may ask to stay in exchange for some sort of favour, or get fired on the spot.
                    mc.name "I'll be honest with you [the_person.title], your performance here at [mc.business.name] leaves a lot to be desired."
                    mc.name "I've been running the numbers and I think we'd be better off without you. Unless you can convince me otherwise I'm going to have to let you go."
                    if the_person.current_job.job_happiness_score > -10:
                        if the_person.effective_sluttiness() < 20:
                            the_person "No sir, I really need this job. What if I took a pay cut? Would that be enough?"
                            menu:
                                "Cut her pay\n{menu_green}Profit: $[cut_amount:.2f] / day{/menu_green}":
                                    mc.name "If you're willing to take a pay cut I think I can keep you around and see if your performance improves."
                                    $ the_person.current_job.salary -= cut_amount
                                    $ the_person.change_stats(happiness = -(10 - mc.charisma), obedience = -(5-mc.charisma))
                                    the_person "Thank you sir! Thank you so much!"

                                "Fire her":
                                    mc.name "I'm sorry, but that wouldn't be enough."
                                    the_person "I understand. I'll clear out my desk."
                                    $ the_person.change_stats(happiness = -20, obedience = -10)
                                    $ mc.business.remove_employee(the_person)
                        else:
                            if the_person.effective_sluttiness() < 30: #Willing to show her tits.
                                the_person "Wait, I really need this job! There must be something about me that's worth keeping around."
                                $ mc.change_locked_clarity(5)
                                the_person "Just tell me what it is and I'll show it to you..."
                            elif the_person.effective_sluttiness() < 45: #Willing to suck you off, jerk you off.
                                the_person "I can be very convincing [the_person.mc_title]."
                                $ mc.change_locked_clarity(5)
                                the_person "Just tell me what I need to do and I'll do it."
                            elif the_person.effective_sluttiness() < 60: #Willing to fuck you
                                $ mc.change_locked_clarity(5)
                                the_person "I don't think my value here is really captured by performance quotas... Let me remind you why you really keep me around."
                            else:
                                $ mc.change_locked_clarity(10)
                                the_person "I'm not just here for the job though, I'm here for you [the_person.mc_title]."
                                the_person "What do I need to do to convince you to keep me around? I'll do anything at all for you."

                            menu:
                                "Make her strip":
                                    mc.name "Fine, I'll reconsider. In exchange, I want you to strip for me."
                                    if the_person.has_taboo("underwear_nudity"):
                                        the_person "[the_person.mc_title], is that really what it's going to take?"
                                        mc.name "It's the only chance you've got right now. Hurry up, I don't have all day and I'm running out of patience."
                                        $ the_person.change_stats(love = -1, obedience = 2 + the_person.opinion.being_submissive)
                                        "She takes a deep breath, then begins to undress."
                                    else:
                                        the_person "Well, if that's what it's going to take I guess I have no choice..."
                                        $ the_person.change_obedience(1)

                                    $ generalised_strip_description(the_person, the_person.outfit.get_underwear_strip_list())
                                    $ mc.change_locked_clarity(10)
                                    if the_person.has_taboo("underwear_nudity") or (the_person.has_taboo("bare_pussy") and the_person.vagina_visible) or (the_person.has_taboo("bare_tits") and the_person.tits_visible):
                                        the_person "Is this what you wanted to see? Are we done?"
                                        "[the_person.title] tries to cover herself up with her hands, shuffling nervously in front of your desk."
                                        $ the_person.update_outfit_taboos()

                                    else: #No taboo broken, no big deal
                                        the_person "Is this what you wanted to see [the_person.mc_title]? I hope it's worth keeping me around..."

                                    if not (the_person.vagina_visible and the_person.tits_visible):
                                        menu:
                                            "Make her strip naked" if the_person.obedience > 110:
                                                mc.name "You aren't finished yet. Keep stripping, I want to see you naked."
                                                $ remove_shoes = False
                                                $ the_item = the_person.outfit.get_feet_top_layer
                                                if the_item:
                                                    the_person "Do you want me to keep my [the_item.display_name] on?"
                                                    menu:
                                                        "Strip it all off":
                                                            mc.name "Take it all off, I don't want you to be wearing anything."
                                                            $ remove_shoes = True

                                                        "Leave them on":
                                                            mc.name "You can leave them on."
                                                $ the_item = None

                                                if the_person.has_taboo(["bare_pussy", "bare_tits"]):
                                                    the_person "I... I'm not sure [the_person.mc_title]."
                                                    mc.name "I'm not going to bother asking twice."
                                                    the_person "Fine! I'll do it..."
                                                else:
                                                    "She nods obediently."
                                                    the_person "Yes [the_person.mc_title], whatever you want."

                                                $ generalised_strip_description(the_person, the_person.outfit.get_full_strip_list(strip_feet = remove_shoes))

                                            "Make her strip naked\n{menu_red}Requires: 110 Obedience{/menu_red} (disabled)" if the_person.obedience < 110:
                                                pass

                                            "Move on":
                                                pass

                                    if the_person.update_outfit_taboos():
                                        the_person "Are... Are we done now? Can I get dressed?"
                                        mc.name "Not yet. Turn around, I want to get a look at your ass."
                                        mc.name "And stop trying to cover yourself up. The point is for me to look at you, right?"
                                        $ the_person.draw_person(position = "back_peek")
                                        $ mc.change_locked_clarity(10)
                                        "[the_person.possessive_title!c] reluctantly follows your instructions, letting her hands drop to her sides and turning around."
                                        "She stands rigidly at first, but as the seconds tick by silently seems to grow more comfortable."
                                    else:
                                        the_person "Well, now what?"
                                        mc.name "Turn around, let me take a look at your ass."
                                        $ the_person.draw_person(position = "back_peek")
                                        $ mc.change_locked_clarity(10)
                                        "[the_person.possessive_title!c] obediently follows your instructions. She bounces her hips, jiggling her butt as you ogle her."

                                    mc.name "Okay, that's enough."
                                    $ the_person.draw_person()
                                    the_person "So... I'm not being fired?"
                                    "You shake your head."
                                    mc.name "Not today, at least. I expect to see improvements, or we'll be back here and I won't be so understanding."
                                    $ the_person.change_stats(happiness = -5 + the_person.opinion(("showing her tits", "showing her ass")), obedience = 3 + the_person.opinion.being_submissive, slut = 1, max_slut = 40)
                                    the_person "Understood. I'll be doing next time, I promise!"
                                    "[the_person.possessive_title!c] collects her clothing and gets dressed."
                                    $ the_person.apply_planned_outfit(show_dress_sequence = True)

                                "Make her jerk you off" if the_person.is_willing(handjob):
                                    "You nod thoughtfully, then roll your office chair back away from your desk."
                                    mc.name "Alright then, I'll make you a deal."
                                    the_person "Thank you [the_person.mc_title]! What do I need to do?"
                                    "You unzip your pants and pull out your half-hard cock."
                                    mc.name "I want you to jerk me off. I should be getting something for my money, right?"
                                    if the_person.has_taboo("touching_penis"):
                                        the_person "You want me to give you a... handjob?"
                                        $ the_person.draw_person(position = "stand3")
                                        "[the_person.possessive_title!c] seems unsure, but she takes a few shaky steps towards you."
                                        $ mc.change_locked_clarity(5)
                                        the_person "And if I do this you won't fire me?"
                                        mc.name "That's the deal. Come on, it doesn't bite."
                                    else:
                                        the_person "I give you a handjob and you won't fire me?"
                                        $ the_person.draw_person(position = "stand3")
                                        "She walks to your side of the desk, eyes fixed on your cock."
                                        mc.name "That's the deal. It doesn't seem too hard, does it?"
                                        $ mc.change_locked_clarity(10)
                                        the_person "Oh, that looks plenty hard... Fine, I'll do it."

                                    #TODO: We really need a sitting and kneeling handjob pose.
                                    "[the_person.possessive_title!c] stands in front of you and reaches out, gently wrapping her fingers around your shaft."
                                    if the_person.break_taboo("touching_penis"):
                                        "She gasps when your cock twitches in response."
                                        mc.name "Relax, just do what comes naturally. A woman like you should know what to do with a cock in her hand."

                                    else:
                                        "She laughs when your cock twitches in response."
                                        the_person "Oh my god, happy to see me little guy?"
                                        mc.name "Hey, it's not that little."
                                        the_person "It's certainly not..."

                                    $ mc.change_locked_clarity(10)
                                    "[the_person.title] starts to stroke it, rhythmically running her hand up and down your length."
                                    call fuck_person(the_person, private = True, start_position = handjob, start_object = make_floor(), girl_in_charge = True, skip_intro = True, position_locked = True) from _call_fuck_person_12
                                    $ the_report = _return
                                    $ the_person.draw_person(position = "sitting")
                                    "[the_person.possessive_title!c] sits back and rubs her arm."
                                    if the_report.get("guy orgasms", 0) > 0:
                                        the_person "Whew, that's an arm workout!"
                                        the_person "So... We have an understanding?"
                                        mc.name "For now. If your performance doesn't improve you're going to have to work even harder to convince me."
                                        the_person "It won't happen again, I promise!"

                                    else:
                                        the_person "I can't do it [the_person.mc_title]... I tried, I swear I tried!"
                                        if office_punishment.is_active:
                                            menu:
                                                "Punish her for under performing":
                                                    mc.name "You did try. I'll be lenient and just write this up as a rules infraction."
                                                    $ the_person.add_infraction(Infraction.underperformance_factory())
                                                    the_person "Thank you [the_person.mc_title]. I'll do better next time."

                                                "Let it go":
                                                    mc.name "You did, that's true. I'll be generous this time, but you better be prepared to finish me next time."
                                                    the_person "Next time? I mean, of course [the_person.mc_title]."

                                        else:
                                            mc.name "You did, that's true. I'll be generous this time, but you better be prepared to finish me next time."
                                            the_person "Next time? I mean, of course [the_person.mc_title]."
                                    $ the_person.change_stats(happiness = -5 + (2*the_person.opinion.giving_handjobs), obedience = 1 + the_person.opinion.being_submissive, slut = 1 + the_person.opinion.giving_handjobs, max_slut = 50)
                                    $ the_person.review_outfit()
                                    $ the_person.draw_person()

                                "Make her jerk you off\n{menu_red=16}Requires: handjob available{/menu_red} (disabled)" if not the_person.is_willing(handjob):
                                    pass

                                "Make her blow you" if the_person.is_willing(blowjob):
                                    "You nod thoughtfully, then roll your office chair back away from your desk."
                                    mc.name "Alright then, I'll make you a deal."
                                    the_person "Thank you [the_person.mc_title]! What do I need to do?"
                                    "You unzip your pants and pull out your hardening cock."
                                    $ mc.change_locked_clarity(10)
                                    mc.name "I want you to suck me off. Do a good job and I'll let you keep your job."
                                    if the_person.has_taboo("sucking_cock"):
                                        the_person "You want a blowjob?"
                                        mc.name "Yeah, I do. You know how to give one, right?"
                                        the_person "Of course! I just wasn't expecting... Well, I don't know what I was expecting."
                                        $ the_person.draw_person(position = "stand3")
                                        "You motion her closer, and she takes a few unsteady steps."
                                        mc.name "Get on your knees. Don't worry, it doesn't bite."
                                        $ mc.change_locked_clarity(10)
                                        $ the_person.draw_person(position = "kneeling1")
                                        "[the_person.possessive_title!c] nods and drops down in front of you."
                                        $ the_person.break_taboo("sucking_cock")
                                    else:
                                        the_person "A blowjob? Well, I guess that's not so bad..."
                                        $ the_person.draw_person(position = "stand3")
                                        "She takes a few steps closer."
                                        mc.name "Get on your knees, I'm getting a little impatient."
                                        $ the_person.draw_person(position = "kneeling1")
                                        $ mc.change_locked_clarity(5)
                                        "[the_person.possessive_title!c] nods and drops down in front of you."

                                    the_person "Just... a blowjob, right?"
                                    mc.name "To start with, at least."

                                    "You present your cock, and she leans forward to take it in her mouth."
                                    $ mc.change_locked_clarity(10)
                                    $ the_person.draw_person(position = "blowjob")
                                    "She sucks on the tip for a few moments, then slides you deeper into her mouth."

                                    if the_person.is_submissive:
                                        $ the_person.add_situational_slut("seduction_approach", 5*the_person.opinion.being_submissive, "He's using me just like a toy!")
                                    else:
                                        $ the_person.add_situational_slut("seduction_approach", -5 + (-5*the_person.opinion.being_submissive), "I'm just a toy to him.")
                                    $ the_person.add_situational_obedience("seduction_approach", 10, "I'll do what I need to keep my job!")
                                    call mc_sex_request(the_person, the_request = "blowjob") from _call_mc_sex_request_employee_performance_review
                                    $ the_person.clear_situational_slut("seduction_approach")
                                    $ the_person.clear_situational_obedience("seduction_approach")

                                    $ the_person.change_stats(happiness = -5 + the_person.opinion.giving_blowjobs, obedience = 1 + the_person.opinion.being_submissive, slut = 2, max_slut = 60)
                                    mc.name "Okay [the_person.title], I'll keep you around for a little while longer, but you're going to need to shape up unless you want this to be a regular occurrence."
                                    $ the_person.review_outfit()
                                    $ the_person.draw_person()
                                    if the_person.effective_sluttiness() < 50:
                                        the_person "I'll do my best sir, I promise."
                                    else:
                                        the_person "Would that really be such a bad thing?"


                                "Make her blow you\n{menu_red=16}Requires: blowjob available{/menu_red} (disabled)" if not the_person.is_willing(blowjob):
                                    pass

                                "Fuck her" if the_person.is_willing(standing_doggy):
                                    mc.name "Is that so? Alright, first things first then. Get naked."
                                    "[the_person.possessive_title!c] doesn't seem to have any problem with the command."
                                    $ remove_shoes = False
                                    $ the_item = the_person.outfit.get_feet_top_layer
                                    if the_item:
                                        the_person "Do you want me to keep my [the_item.display_name] on?"
                                        menu:
                                            "Strip it all off":
                                                mc.name "Take it all off, I don't want you to be wearing anything."
                                                $ remove_shoes = True

                                            "Leave them on":
                                                mc.name "You can leave them on."
                                    $ the_item = None

                                    "She nods obediently."
                                    the_person "Yes [the_person.mc_title], whatever you want."

                                    $ generalised_strip_description(the_person, the_person.outfit.get_full_strip_list(strip_feet = remove_shoes))
                                    $ mc.change_locked_clarity(10)
                                    the_person "Now what?"
                                    "You slide your chair back from your desk and stand up."
                                    mc.name "Now let's see just how committed you are to this job."
                                    "You walk around to her side of the desk and pat the edge."
                                    mc.name "Put your hands here. Keep your legs straight."
                                    $ the_person.draw_person("standing_doggy")
                                    "She follows your instructions obediently, bending over to plant her palms on your desk."
                                    the_person "What are you going to do?"
                                    $ mc.change_locked_clarity(10)
                                    "You walk behind [the_person.title] and unzip your pants. When you pull them down your hard cock springs out and bounces against an ass cheek."
                                    mc.name "I'm going to fuck you. That's not a problem, is it?"
                                    "You hold your shaft and rub the tip of your cock between her legs."
                                    if the_person.wants_condom():
                                        the_person "Wait, wait! If you're going to fuck me... you need to wear a condom!"
                                        menu:
                                            "Put on a condom":
                                                mc.name "Fine, but I'm going to have to really lay into you so I can feel anything."
                                                the_person "Thank you [the_person.mc_title]."
                                                "You drag your tip teasingly across the slit of her pussy, then step back and pull a condom out of your wallet."
                                                $ mc.condom = True
                                                "You spread it over your dick, then step into position and line yourself up."

                                            "Fuck her raw anyways" if the_person.obedience >= 130:
                                                mc.name "That's where you draw the line? You'll fuck your boss to keep your job, but you need a tiny bit of latex?"
                                                mc.name "No, I'm going to feel that hot pussy wrapped around my cock raw."
                                                if not the_person.on_birth_control:
                                                    the_person "I'm not on the pill [the_person.mc_title]..."
                                                    $ the_person.update_birth_control_knowledge()
                                                    mc.name "Well then, you've got a choice."
                                                    mc.name "You can walk out of this room unemployed, or you can walk out of this room pregnant."
                                                    "You drag the tip teasingly across the lips of her pussy while she thinks."

                                                else:
                                                    the_person "[the_person.mc_title], I really shouldn't..."
                                                    mc.name "You've got two choices [the_person.title]."
                                                    mc.name "You can walk out of this room unemployed, or you can walk out with a pussy full of my cum."
                                                    "You tap the tip of your cock on her clit, teasing her while she thinks."
                                                $ mc.change_locked_clarity(10)
                                                the_person "... Fine... Just this once."
                                                $ the_person.change_obedience(1 + the_person.opinion.being_submissive)
                                                mc.name "Good girl, that's what I like to hear."
                                                "You hold your shaft steady with one hand and line yourself up with her."

                                            "Fuck her raw anyways\n{menu_red=16}Requires: 130 Obedience{/menu_red} (disabled)" if the_person.obedience < 130:
                                                pass

                                    else: #She doesn't want one, but we'll give you the option in case you're trying not to get girls pregnant.
                                        the_person "No [the_person.mc_title], no problem..."
                                        menu:
                                            "Put on a condom":
                                                mc.name "Of course, I need to put a condom on first."
                                                mc.name "I wouldn't want any accidents showing up nine months from now."
                                                "You step back and pull a condom out of your wallet. After a moment of fumbling you have it spread over your dick."
                                                "You hold your shaft with one hand and step close to [the_person.possessive_title] again, teasing the lips of her pussy with your tip."
                                                $ mc.condom = True

                                            "Fuck her raw":
                                                pass

                                    if the_person.has_taboo("vaginal_sex"):
                                        the_person "Wait, I've changed my..."
                                        "It's too late for second thoughts. You plunge your hard dick into [the_person.title]'s tight cunt. She gasps softly under her breath."
                                        $ the_person.break_taboo("vaginal_sex")
                                        the_person "... Mind! Oh fuck..."
                                        "You can hear her mumble: this is happening."
                                    else:
                                        "You push forward, plunging your hard dick into [the_person.title]'s tight cunt. She gasps softly under her breath."

                                    if not mc.condom and the_person.has_taboo("condomless_sex"):
                                        $ the_person.break_taboo("condomless_sex")

                                    "You hold yourself deep inside her and enjoy the sudden warmth around your shaft."
                                    "When you think she's ready you pull your hips back and start to pump in and out of her."

                                    if not mc.condom:
                                        $ the_person.break_taboo("condomless_sex")
                                    if the_person.is_submissive:
                                        $ the_person.add_situational_slut("seduction_approach", 5*the_person.opinion.being_submissive, "He's using me just like a toy!")
                                    else:
                                        $ the_person.add_situational_slut("seduction_approach", -5 + (-5*the_person.opinion.being_submissive), "I'm just a toy to him.")
                                    $ the_person.add_situational_obedience("seduction_approach", 10, "I'll do what I need to keep my job!")
                                    call fuck_person(the_person, private = True, start_position = standing_doggy, start_object = make_desk(), skip_intro = True, skip_condom = True) from _call_fuck_person_mod_only_1

                                    $ the_person.call_dialogue("sex_review", the_report = _return)
                                    $ the_person.clear_situational_slut("seduction_approach")
                                    $ the_person.clear_situational_obedience("seduction_approach")

                                    $ the_person.change_stats(happiness = -4 + the_person.opinion(("sex standing up", "vaginal sex")), obedience = 2 + the_person.opinion(("sex standing up", "vaginal sex")), slut = 2, max_slut = 80)
                                    $ the_person.review_outfit()
                                    $ the_person.draw_person()
                                    mc.name "Okay [the_person.title], I'll keep you around for a little while longer, but you're going to need to shape up unless you want this to be a regular occurrence."
                                    if the_person.effective_sluttiness() < 50:
                                        the_person "I'll do my best sir, I promise."
                                    else:
                                        the_person "Would that really be such a bad thing?"


                                "Fuck her\n{menu_red}Requires: sex available{/menu_red} (disabled)" if not the_person.is_willing(standing_doggy):
                                    pass

                                "Fire her":
                                    mc.name "I'm sorry, but I don't think there's anything you can do to convince me."
                                    mc.name "Collect your things and get out."
                                    "[the_person.possessive_title!c] seems slightly stunned, but nods without any more complaints."
                                    $ the_person.change_stats(happiness = -20, obedience = -10)
                                    $ mc.business.remove_employee(the_person)


                    else:
                        $ the_person.draw_person(position = "sitting", emotion = "angry")
                        the_person "What? You want me to beg to stay at this shitty job? If you don't want me here I think it's best I just move on. I quit!"
                        $ clear_scene()
                        "[the_person.title] stands up and storms out."
                        $ mc.business.remove_employee(the_person)
                        call advance_time() from _call_advance_time_13
                        return

                "Punish her sexually" if the_person.effective_sluttiness() >= 40 and the_person.obedience >= 120: #Orgasm denial and/or make her service you.
                    "You sigh dramatically and stand up from your desk. You walk over to the other side and sit on the corner nearest [the_person.title]."
                    mc.name "Your performance has really let me down, but I think what you need a little motivation."
                    mc.name "I want to have some fun with you, but you're not allowed to climax, is that understood?"
                    $ opinion_modifier = the_person.opinion.being_submissive * 5
                    $ the_person.add_situational_slut("seduction_approach", -5 + opinion_modifier, "I'm just being used...")
                    $ the_person.add_situational_obedience("seduction_approach", 10 + opinion_modifier, "I'm being punished")
                    the_person "I... if you think this is what I need, sir."
                    call fuck_person(the_person,private = True) from _call_fuck_person_13
                    $ the_report = _return
                    $ the_person.clear_situational_slut("seduction_approach")
                    $ the_person.clear_situational_obedience("seduction_approach")
                    if the_report.get("girl orgasms", 0) > 0: #We made her cum! Congratulations!
                        $ the_person.change_stats(happiness = 5, obedience = -10, slut = 1, max_slut = 60)
                        the_person "You just can't resist pleasing me, can you [the_person.mc_title]? I thought I wasn't supposed to cum?"
                        "[the_person.title] seems smug about her orgasmic victory."

                    elif the_report.get("end arousal", 0) >= 80:
                        $ the_person.change_stats(happiness = 5, obedience = -5, slut = 2, max_slut = 60)
                        the_person "Oh my god [the_person.mc_title], you got me so close... Can't you just finish me off, real quick?"
                        mc.name "Do a better job and I'll let you cum next time. Understood?"
                        "[the_person.title] nods meekly."
                    else:
                        $ the_person.change_stats(happiness = -5, obedience = 5, slut = -1, max_slut = 60)
                        mc.name "That felt great [the_person.title], I suppose if your performance doesn't improve you'll still be useful as a toy."
                        the_person "I... Yes sir, I suppose I would be."

                    $ the_person.review_outfit()
                    $ the_person.draw_person()

                "Record an infraction" if office_punishment.is_active:
                    mc.name "Your performance lately has been less than stellar. I hope the problem is simply a matter of discipline, which I can correct."
                    mc.name "I'm going to take some time to think about what punishment would be suitable."
                    $ the_person.add_infraction(Infraction.underperformance_factory())
                    if the_person.current_job.job_happiness_score > 0:
                        the_person "I can improve [the_person.mc_title], I promise."

                    else:
                        the_person "I... Fine, I understand."
                    mc.name "Good to hear it."

        "Finish the performance review":
            mc.name "Well, I think you're doing a perfectly adequate job around here [the_person.title]. If you keep up the good work I don't think we will have any issues."
            $ the_person.change_stats(happiness = 2, obedience = 1)
            the_person "Thank you, I'll do my best."

    $ the_person.draw_person()
    "You stand up and open the door for [the_person.title] at the end of her performance review."
    if the_person.is_employee:
        mc.name "Now let's get back to work."
        the_person "Of course, [the_person.mc_title]."
    else:
        mc.name "I wish you all the best for the future."
        the_person "Yeah, I guess."
    $ mc.business.listener_system.fire_event("general_work")
    $ clear_scene()
    call advance_time() from _call_advance_time_14
    return

label employee_salary_improvement_options(the_person):
    $ market_rate = the_person.current_job.base_salary - the_person.current_job.salary
    $ raise_amount10 = builtins.max(the_person.current_job.salary * .1, 2) # minimum raise $2
    $ raise_amount25 = builtins.max(the_person.current_job.salary * .25, 2) # minimum raise $2
    $ raise_amount50 = builtins.max(the_person.current_job.salary * .5, 2) # minimum raise $2
    menu:
        "Adjust to market rate\n{menu_green}$[market_rate:+.2f]/day{/menu_green}" if market_rate >= 2:
            mc.name "I've been very impressed by your work lately, and I'd like to make sure you stay happy with your decision to work here."
            mc.name "I'm going to raise your salary to $[market_rate:.2f] per day. How does that sound?"
            $ the_person.current_job.salary += market_rate
            $ the_person.change_stats(love = 2, happiness = 5)
            the_person "That sounds amazing! Thank you sir, I promise I won't let you down!"
            mc.name "Good to hear it."

        "Adjust to market rate\n{menu_green}$[market_rate:+.2f]/day{/menu_green}\n{menu_red}Insufficient Increase of Salary{/menu_red} (disabled)" if market_rate < 2:
            pass

        "Give her a 10%% raise\n{menu_green}$[raise_amount10:+.2f]/day{/menu_green}":
            mc.name "I've been very impressed by your work lately, and I'd like to make sure you stay happy with your decision to work here."
            mc.name "I'm going to put you down for a 10%% raise. How does that sound?"
            $ the_person.current_job.salary += raise_amount10
            $ the_person.change_stats(love = 2, happiness = 10)
            the_person "That sounds amazing! Thank you sir, I promise I won't let you down!"
            mc.name "Good to hear it."
        "Give her a 25%% raise\n{menu_green}$[raise_amount25:+.2f]/day{/menu_green}":
            mc.name "I've been noticing you putting in a lot of extra effort in your work here, and I am very impressed. I want to make sure your efforts are adequately reflected in your salary, and make sure you stay happy with your decision to work here."
            mc.name "I'm going to put you down for a 25%% raise. How does that sound?"
            $ the_person.current_job.salary += raise_amount25
            $ the_person.change_stats(love = 5, happiness = 15, obedience = 1)
            "She is taken aback by the amount, and it takes her a moment to respond."
            the_person "I... That sounds amazing! Thank you Sir!"
            the_person "I knew I made the right choice coming to work here. I promise I won't let you down!"
            mc.name "Good to hear it."
        "Give her a 50%% raise\n{menu_green}$[raise_amount50:+.2f]/day{/menu_green}":
            mc.name "You have substantially surpassed my expectations. I believe your talents are worth much more than what is currently reflected by your salary, and I'd like to make sure you stay happy with your decision to work here."
            mc.name "I'm going to put you down for a 50%% raise. How does that sound?"
            $ the_person.current_job.salary += raise_amount50
            $ the_person.change_stats(love = 8, happiness = 20, obedience = 3)
            "She stares at you in disbelief for a moment, before finally bursting out in excitement."
            the_person "Wow, really!? That sounds amazing! Thank you, sir!"
            $ the_person.draw_person(position = "kissing")
            "She jumps up and rushes to you and gives you a big hug, with the beginning of tears forming in her eye."
            the_person "Thank you Sir, this means so much to me. I promise I won't let you down!"
            mc.name "Good to hear it."
            $ the_person.draw_person()
    return

label move_employee_label(the_person):
    if the_person == mc.business.head_researcher:
        "Moving [the_person.title] will remove her from her role as head researcher. Are you sure you want to move [the_person.title]?"
        menu:
            "Yes, move [the_person.title]":
                pass
            "No, leave [the_person.title]":
                $ clear_scene()
                return

    the_person "Where would you like me then?"

    menu:
        "Research and Development":
            $ mc.business.add_employee_research(the_person, start_day = day)
            $ the_person.change_location(mc.business.r_div)
        "Production":
            $ mc.business.add_employee_production(the_person, start_day = day)
            $ the_person.change_location(mc.business.p_div)
        "Supply Procurement":
            $ mc.business.add_employee_supply(the_person, start_day = day)
            $ the_person.change_location(mc.business.s_div)
        "Marketing":
            $ mc.business.add_employee_marketing(the_person, start_day = day)
            $ the_person.change_location(mc.business.m_div)
        "Human Resources":
            $ mc.business.add_employee_hr(the_person, start_day = day)
            $ the_person.change_location(mc.business.h_div)

    call initial_set_duties_label(the_person) from _call_initial_set_duties_move_employee

    mc.name "Good, ready to get started?"
    the_person "I'll move over there right away!"
    return

label employee_paid_serum_test_label(the_person):
    $ pay_serum_cost = 100
    mc.name "[the_person.title], we're running field trials and you're one of the test subjects. I'm going to need you to take this, a bonus will be added onto your paycheck."
    call give_serum(the_person) from _call_give_serum_18
    if _return:
        $ mc.business.change_funds(-pay_serum_cost, stat = "Serum Testing")
        $ mc.stats.change_tracked_stat("Employee", "Paid Serum Tests", 1)
    return

label employee_unpaid_serum_test_label(the_person):
    mc.name "[the_person.title], we're running field trials and you're one of the test subjects. I'm going to need you to take this."
    call give_serum(the_person) from _call_give_serum_19
    if _return:
        $ mc.stats.change_tracked_stat("Employee", "Un-paid Serum Tests", 1)
    return

label employee_punishment_hub(the_person):
    $ selected_infraction = renpy.display_menu(get_infraction_list_menu(the_person), True, "Choice")
    if not isinstance(selected_infraction, Infraction):
        return

    call screen main_choice_display(build_menu_items(build_employee_infraction_choice_menu(the_person, selected_infraction)))
    if not isinstance(_return, Action):
        call employee_punishment_hub(the_person) from _call_employee_punishment_hub
    else:
        $ _return.call_action([the_person, selected_infraction])

        python:
            the_person.remove_infraction(selected_infraction)
            ran_num = -2*(selected_infraction.severity - the_person.opinion.being_submissive)
            the_person.change_happiness(ran_num if ran_num > 0 else 0)
            the_person.set_event_day("last_punished")
            mc.stats.change_tracked_stat("Employee", "Punishments", 1)

    $ selected_infraction = None
    return

label employee_generate_infraction_label(the_person):
    $ mc.business.change_team_effectiveness(-5)
    mc.name "[the_person.title], I was reviewing your work and I've found some discrepancies."
    the_person "Oh, I'm sorry [the_person.mc_title], I..."
    mc.name "Unfortunately company policy requires I write you up for it. Don't worry, everyone makes mistakes."
    $ the_person.change_happiness(-5)
    "She frowns, but nods obediently."
    $ the_person.add_infraction(Infraction.bureaucratic_mistake_factory())
    return

label request_promotion_crisis_label(the_person):
    $ the_person.draw_person()
    if not mc.is_at(ceo_office):
        the_person "[the_person.mc_title], can we talk in your office for a second?"
        $ mc.change_location(ceo_office)
        "You nod and take her into your office, closing the door behind you. You take a seat and motion for her to do the same."
    else:
        the_person "[the_person.mc_title], can we talk for a second?"
        "You nod and gesture to a chair for her to sit down."

    $ the_person.draw_person(position = "sitting")
    #TODO: Make this personality based
    mc.name "What do you need [the_person.title]?"
    the_person "It's about my work here at [mc.business.name], I think I'm ready to take on more responsibility."
    "She hesitates before pushing further."
    the_person "...And I would want a raise to go with those new responsibilities."
    mc.name "You're looking for a promotion?"

    $ ran_num = the_person.current_job.seniority_level + 1
    menu:
        "Promote her\n{menu_green}+1 Work Experience (To level [ran_num]){/menu_green}":
            "You think about it for a second, then nod approvingly."
            mc.name "You're right, of course."
            call promotion_after_convince(the_person) from _call_promotion_after_convince
            $ the_person.draw_person(position = "walking_away")
            "She stands up and heads for the door."
            $ the_person.draw_person(position = "back_peek")
            the_person "Thank you for your time."
            "You nod, and she leaves your office."
            $ mc.stats.change_tracked_stat("Employee", "Promotions", 1)

        "Refuse":
            mc.name "I don't think you're ready for that [the_person.title]. Show me you're really dedicated and we can talk about this in the future, okay?"
            $ apply_sex_modifiers(the_person)
            if the_person.effective_sluttiness() + the_person.opinion.taking_control * 5 < 40 and not the_person.is_affair: #Girls you are having an affair with will always try to seduce you and get a promotion.
                $ the_person.change_happiness(-5 + 3 * the_person.opinion.being_submissive)
                the_person "I understand... Sorry to have bothered you."
                $ the_person.draw_person(position = "walking_away")
                "She leaves your office, clearly unhappy with the results."
            else:
                $ the_person.draw_person()
                "[the_person.possessive_title!c] seems disappointed. She stands up and places her hands on your desk, leaning forward."
                if the_person.has_large_tits:
                    "The pose accentuates her large breasts, threatening to pull your attention away from the conversation."
                the_person "Isn't there... some way I could show you how dedicated I really am? Something just between the two of us?"
                menu:
                    "Make her strip":
                        "You sit forward. She has your attention."
                        mc.name "Fine, I'll reconsider. In exchange, I want you to strip for me."
                        if the_person.has_taboo("underwear_nudity"):
                            "[the_person.title] seems nervous, but she must have expected something like this. She gathers her nerves and begins to undress."
                            $ the_person.change_obedience(1)
                        else:
                            the_person "Right away, sir."

                        $ generalised_strip_description(the_person, the_person.outfit.get_underwear_strip_list())
                        $ mc.change_locked_clarity(10)
                        if the_person.has_taboo("underwear_nudity") or (the_person.has_taboo("bare_pussy") and the_person.vagina_visible) or (the_person.has_taboo("bare_tits") and the_person.tits_visible):
                            the_person "Well... There you go."
                            "[the_person.title] tries to cover herself up with her hands, shuffling nervously in front of your desk."
                            $ the_person.update_outfit_taboos()

                        else: #No taboo broken, no big deal
                            the_person "Satisfied [the_person.mc_title]? Have I earned my promotion?"

                        if not (the_person.vagina_visible and the_person.tits_visible):
                            menu:
                                "Make her strip naked" if the_person.obedience > 110:
                                    mc.name "You aren't finished yet. Keep stripping, I want to see you naked."
                                    $ remove_shoes = False
                                    $ feet_ordered = the_person.outfit.get_feet_ordered()
                                    if feet_ordered:
                                        $ top_feet = feet_ordered[-1]
                                        the_person "Do you want me to keep my [top_feet.display_name] on?"
                                        menu:
                                            "Strip it all off":
                                                mc.name "Take it all off, I don't want you to be wearing anything."
                                                $ remove_shoes = True

                                            "Leave them on":
                                                mc.name "You can leave them on."

                                    if the_person.has_taboo(["bare_pussy", "bare_tits"]):
                                        the_person "I... I'm not sure [the_person.mc_title]."
                                        mc.name "You didn't do all this just to waste my time, did you?"
                                        "She shakes her head."
                                        the_person "No, no! I'll do it..."
                                    else:
                                        "She nods obediently."
                                        the_person "Yes [the_person.mc_title]. I'll show you that I'm very good at following instructions."

                                    $ generalised_strip_description(the_person, the_person.outfit.get_full_strip_list(strip_feet = remove_shoes))

                                "Make her strip naked\n{menu_red}Requires: 110 Obedience{/menu_red} (disabled)" if the_person.obedience < 110:
                                    pass

                                "Move on":
                                    pass

                        if the_person.update_outfit_taboos():
                            the_person "Have... you seen everything you wanted to see?"
                            mc.name "Not yet. Turn around, I want to get a look at your ass."
                            "[the_person.title] nods meekly."
                            mc.name "And stop trying to cover yourself up. The point is for me to look at you, right?"
                            $ the_person.draw_person(position = "back_peek")
                            $ mc.change_locked_clarity(10)
                            "[the_person.possessive_title!c] nods again and follows your instructions, letting her hands drop to her sides and turning around."
                        else:
                            the_person "What now, sir?"
                            mc.name "Turn around, let me take a look at your ass."
                            $ the_person.draw_person(position = "back_peek")
                            $ mc.change_locked_clarity(10)
                            "[the_person.possessive_title!c] obediently follows your instructions. She bounces her hips, jiggling her butt as you ogle her."

                        mc.name "Okay, you've proved your point."
                        call promotion_post_sex_convince(the_person) from _call_promotion_post_sex_convince_1
                        if _return:
                            "[the_person.possessive_title!c] collects her clothing and gets dressed."
                            $ the_person.apply_planned_outfit(show_dress_sequence = True)
                            the_person "Thank you for this opportunity [the_person.mc_title], I won't let you down."
                        else:
                            $ the_person.apply_planned_outfit(show_dress_sequence = True)
                            $ the_person.draw_person(position = "walking_away")
                            "[the_person.possessive_title!c] gets dressed and storms out of your office without another word."

                    "Make her jerk you off" if the_person.is_willing(cowgirl_handjob):
                        "You nod thoughtfully, then roll your office chair back away from your desk."
                        mc.name "Alright then, I'll make you a deal."
                        "You unzip your pants and pull out your half-hard cock."
                        mc.name "I want you to show me just how dedicated you are by jerking me off."
                        if the_person.has_taboo("touching_penis"):
                            the_person "You want me to give you a... handjob?"
                            "[the_person.possessive_title!c] seems unsure, but she takes a few shaky steps towards you."
                            $ mc.change_locked_clarity(5)
                            the_person "And if I do that, you'll give me my promotion?"
                            mc.name "That's the deal. Come on, touch it."
                        else:
                            the_person "I give you a handjob, you give me my promotion?"
                            "She walks to your side of the desk, eyes fixed on your cock."
                            mc.name "That's the deal. Doesn't seem too hard, does it?"
                            $ mc.change_locked_clarity(10)
                            the_person "Oh, that looks plenty hard... but I think I can manage."

                        #TODO: We really need a sitting and kneeling handjob pose.
                        $ the_person.draw_person(position = "kneeling1")
                        "[the_person.possessive_title!c] kneels down in front of you and reaches out, gently wrapping her fingers around your shaft."
                        if the_person.break_taboo("touching_penis"):
                            "She gasps when your cock twitches in response, but quickly regains her composure and laughs."
                            the_person "Sorry, I'm just a little nervous..."
                            mc.name "Don't worry, I'm sure this will all come naturally to you."

                        else:
                            "She laughs when your cock twitches in response."
                            the_person "Oh my god, happy to see me little guy?"
                            mc.name "Hey, it's not that little."
                            the_person "It's certainly not..."

                        $ mc.change_locked_clarity(10)
                        "[the_person.title] starts to stroke it, rhythmically running her hand up and down your length."
                        call fuck_person(the_person, private = True, start_position = cowgirl_handjob, girl_in_charge = True, skip_intro = True, position_locked = True) from _call_fuck_person_139
                        $ the_report = _return
                        $ the_person.draw_person(position = "kneeling1")
                        "[the_person.possessive_title!c] sits back and rubs her arm."
                        if the_report.get("guy orgasms", 0) > 0:
                            the_person "Well, do we have an understanding [the_person.mc_title]?"

                        else:
                            the_person "I can't do it [the_person.mc_title]... I tried, I swear I tried!"
                            if office_punishment.is_active:
                                menu:
                                    "Punish her for under performing":
                                        mc.name "You did try. I'll be lenient and just write this up as a rules infraction."
                                        $ the_person.add_infraction(Infraction.underperformance_factory())
                                        "[the_person.possessive_title!c] stares at you slack-jawed."
                                        the_person "What? But I..."
                                        mc.name "Made a promise of performance and failed to deliver. It's all in the company handbook."
                                        "She frowns but drops the subject."

                                    "Let it go":
                                        mc.name "I know, I'll keep that in mind."

                            else:
                                mc.name "I know, I'll keep that in mind."

                        $ the_person.draw_person()
                        "She brushes her knees off and stands up, ready to continue the negotiations."
                        call promotion_post_sex_convince(the_person) from _call_promotion_post_sex_convince_2
                        if _return:
                            the_person "Thank you for this opportunity [the_person.mc_title], I won't let you down."
                            $ the_person.draw_person(position = "walking_away")
                            "Without another word, she turns around and walks out of your office."

                        else:
                            $ the_person.draw_person(position = "walking_away")
                            "[the_person.possessive_title!c] storms out of your office without another word."

                    "Make her jerk you off\n{menu_red}Requires: handjob available{/menu_red} (disabled)" if not the_person.is_willing(handjob):
                        pass

                    "Make her blow you" if the_person.is_willing(blowjob):
                        "You nod thoughtfully, then roll your office chair back away from your desk."
                        mc.name "Alright then, I'll make you a deal."
                        the_person "I knew you could be convinced..."
                        "You unzip your pants and pull out your hardening cock."
                        $ mc.change_locked_clarity(10)
                        mc.name "I want you to suck me off. Do a good job and we can keep talking about that promotion."
                        if the_person.has_taboo("sucking_cock"):
                            the_person "You want a blowjob?"
                            mc.name "Yeah, I do. You know how to give one, right?"
                            the_person "Of course! I just wasn't expecting... Well, I don't know what I was expecting."
                            "You motion her closer, and she takes a few unsteady steps."
                            mc.name "Get on your knees. Don't worry, it doesn't bite."
                            $ mc.change_locked_clarity(10)
                            $ the_person.draw_person(position = "kneeling1")
                            "[the_person.possessive_title!c] nods and drops down in front of you."
                            $ the_person.break_taboo("sucking_cock")
                        else:
                            the_person "Is that what you want, sir? A little blowjob?"
                            "She takes a few steps closer."
                            mc.name "Get on your knees, I don't appreciate being left waiting."
                            $ the_person.draw_person(position = "kneeling1")
                            $ mc.change_locked_clarity(5)
                            "[the_person.possessive_title!c] nods and drops down in front of you."

                        the_person "Just... a blowjob, right?"
                        mc.name "To start with. Now get sucking."

                        "You present your cock, and she leans forward to take it in her mouth."
                        $ mc.change_locked_clarity(10)
                        "She sucks on the tip for a few moments, then slides you deeper into her mouth."

                        if the_person.is_dominant:
                            $ the_person.add_situational_slut("seduction_approach", 5*the_person.opinion.taking_control, "I'll make him do just what ! want!")
                        elif the_person.is_submissive:
                            $ the_person.add_situational_slut("seduction_approach", -5 + (-5*the_person.opinion.taking_control), "I guess I need to do this to convince him...")
                        call mc_sex_request(the_person, the_request = "blowjob") from _call_mc_sex_request_request_promotion_crisis
                        $ the_person.clear_situational_slut("seduction_approach")

                        $ the_person.review_outfit()

                        call promotion_post_sex_convince(the_person) from _call_promotion_post_sex_convince_3
                        if _return:
                            the_person "Thank you for this opportunity [the_person.mc_title], I won't let you down."
                        else:
                            $ the_person.draw_person(position = "walking_away")
                            "[the_person.possessive_title!c] storms out of your office without another word."

                    "Make her blow you\n{menu_red}Requires: blowjob available{/menu_red} (disabled)" if not the_person.is_willing(blowjob):
                        pass

                    "Fuck her" if the_person.is_willing(standing_doggy):
                        mc.name "Alright then, let's see how committed to this you really are."
                        "She smiles, happy to have you change your mind."
                        mc.name "First things first then. Get naked."
                        "[the_person.possessive_title!c] doesn't seem to have any problem with the command."
                        $ remove_shoes = False
                        $ feet_ordered = the_person.outfit.get_feet_ordered()
                        if feet_ordered:
                            $ top_feet = feet_ordered[-1]
                            the_person "Do you want me to keep my [top_feet.display_name] on?"
                            menu:
                                "Strip it all off":
                                    mc.name "Take it all off, I don't want you to be wearing anything."
                                    $ remove_shoes = True

                                "Leave them on":
                                    mc.name "You can leave them on."

                        "She nods obediently."
                        the_person "Yes [the_person.mc_title], whatever you want."

                        $ generalised_strip_description(the_person, the_person.outfit.get_full_strip_list(strip_feet = remove_shoes))
                        $ mc.change_locked_clarity(10)
                        the_person "Now what?"
                        "You slide your chair back from your desk and stand up."
                        mc.name "Now for a little test..."
                        "You walk around to her side of the desk and pat the edge."
                        mc.name "Put your hands here. Keep your legs straight."
                        $ the_person.draw_person("standing_doggy")
                        "She follows your instructions obediently, bending over to plant her palms on your desk."
                        the_person "What are you going to do?"
                        $ mc.change_locked_clarity(10)
                        "You walk behind [the_person.title] and unzip your pants. When you pull them down your hard cock springs out and bounces against an ass cheek."
                        mc.name "I'm going to fuck you. That's not a problem, is it?"
                        "You hold your shaft and rub the tip of your cock between her legs."
                        if the_person.wants_condom():
                            the_person "Wait, wait! If you're going to fuck me... you need to wear a condom!"
                            menu:
                                "Put on a condom":
                                    mc.name "Fine, but I'm going to have to really lay into you if I want to feel anything...."
                                    the_person "Thank you [the_person.mc_title]."
                                    "You drag your tip teasingly across the slit of her pussy, then step back and pull a condom out of your wallet."
                                    $ mc.condom = True
                                    "You spread it over your dick, then step into position and line yourself up."

                                "Fuck her raw anyways" if the_person.obedience >= 150:
                                    mc.name "That's where you draw the line? You'll fuck your boss for a promotion, but you need a tiny bit of latex?"
                                    mc.name "No, I'm going to feel that hot pussy wrapped around my cock raw."
                                    if not the_person.on_birth_control:
                                        the_person "I'm not on the pill [the_person.mc_title]..."
                                        $ the_person.update_birth_control_knowledge()
                                        mc.name "Well then, you've got a choice."
                                        mc.name "Are you willing to get knocked up for your this job?"
                                        "You drag the tip teasingly across the lips of her pussy while she thinks."

                                    else:
                                        the_person "[the_person.mc_title], I really shouldn't..."
                                        mc.name "You've got two choices [the_person.title]."
                                        mc.name "You can walk out of here right now, or you can walk out in a few minutes with a pussy full of my cum."
                                        "You tap the tip of your cock on her clit, teasing her while she thinks."
                                        mc.name "Only one of those options earns you your promotion..."
                                    $ mc.change_locked_clarity(10)
                                    the_person "... Fine... Just this once."
                                    $ the_person.change_obedience(1 + the_person.opinion.being_submissive)
                                    mc.name "Good girl, that's what I like to hear."
                                    "You hold your shaft steady with one hand and line yourself up with her."

                                "Fuck her raw anyways.\nRequires: 150 Obedience (disabled)" if the_person.obedience < 150:
                                    pass

                        else: #She doesn't want one, but we'll give you the option in case you're trying not to get girls pregnant.
                            the_person "No [the_person.mc_title], no problem..."
                            menu:
                                "Put on a condom":
                                    mc.name "Of course, I need to put a condom on first."
                                    mc.name "I wouldn't want any accidents showing up nine months from now."
                                    "You step back and pull a condom out of your wallet. After a moment of fumbling you have it spread over your dick."
                                    "You hold your shaft with one hand and step close to [the_person.possessive_title] again, teasing the lips of her pussy with your tip."
                                    $ mc.condom = True

                                "Fuck her raw":
                                    pass

                        if the_person.has_taboo("vaginal_sex"):
                            the_person "Wait, I need a..."
                            "It's too late for second thoughts. You plunge your hard dick into [the_person.title]'s tight cunt. She gasps softly under her breath."
                            $ the_person.break_taboo("vaginal_sex")
                            the_person "... Second! Oh fuck..."
                            "You can hear the understanding in her voice: this is happening."
                        else:
                            "You push forward, plunging your hard dick into [the_person.title]'s tight cunt. She gasps softly under her breath."

                        if not mc.condom and the_person.has_taboo("condomless_sex"):
                            $ the_person.break_taboo("condomless_sex")

                        "You hold yourself deep inside her and enjoy the sudden warmth around your shaft."
                        "When you think she's ready you pull your hips back and start to pump in and out of her."

                        if the_person.is_dominant:
                            $ the_person.add_situational_slut("seduction_approach", 5*the_person.opinion.taking_control, "I'll make him do just what ! want!")
                        elif the_person.is_submissive:
                            $ the_person.add_situational_slut("seduction_approach", -5 + (-5*the_person.opinion.taking_control), "I guess I need to do this to convince him...")

                        call fuck_person(the_person, private = True, start_position = standing_doggy, start_object = make_desk(), skip_intro = True, skip_condom = True) from _call_fuck_person_mod_only_2

                        $ the_person.call_dialogue("sex_review", the_report = _return)
                        $ the_person.clear_situational_slut("seduction_approach")
                        $ the_person.clear_situational_obedience("seduction_approach")

                        "She puts her clothes back on."
                        $ the_person.apply_planned_outfit(show_dress_sequence = True)

                        call promotion_post_sex_convince(the_person) from _call_promotion_post_sex_convince_4
                        if _return:
                            the_person "Thank you for this opportunity [the_person.mc_title], I won't let you down."
                        else:
                            $ the_person.draw_person(position = "walking_away")
                            "[the_person.possessive_title!c] storms out of your office without another word."

                    "Fuck her\n{menu_red}Requires: sex available{/menu_red} (disabled)" if not the_person.is_willing(standing_doggy):
                        pass

                    "Turn her down":
                        mc.name "We're done here. You can go [the_person.title]."
                        the_person "Right, of course, [the_person.mc_title]."
                        $ the_person.draw_person(position = "walking_away")
                        $ the_person.change_stats(love = -2, happiness = -10, obedience = -3)
                        "She's not very happy about it, but she turns around and leaves."


            #TODO: if she's slutty she'll try to seduce you in exchange for the promotion.


            $ clear_sex_modifiers(the_person)

    $ clear_scene()
    return

label promotion_after_convince(the_person, for_sex = False):
    python:
        the_person.set_event_day("last_promotion_day")
        employee_increase_job_experience(the_person)
        the_person.change_happiness(10)
        the_person.draw_person(position = "sitting")

    "[the_person.possessive_title!c] smiles happily and sits down."
    if for_sex:
        the_person "I'm so glad we could come to an agreement [the_person.mc_title]. Now, let's talk about my new salary..."
    else:
        the_person "Excellent! I knew you would agree! Now, about my salary..."

    menu:
        "Give her a raise":
            call employee_salary_improvement_options(the_person) from _call_employee_salary_improvement_options_promotion_after_convince

        "Don't give her a raise":
            mc.name "That is a little harder to deal with. You're clearly an important member of the team, but money is tight right now."
            mc.name "Once you've taken on your new duties and proved you're worth the extra money we can talk about a raise."
            $ the_person.change_stats(love = -2, happiness = -10)
            "[the_person.title] scowls and shakes her head."
            the_person "You want me to do more work for the same money?"
            mc.name "Just for a little while. I promise you'll be the first to receive a raise when we have the budget."
            "She seems unconvinced, but relents and nods."
            the_person "Fine, fine."

    mc.name "Good. Let's talk about your new duties then..."
    call set_duties_controller(the_person, the_person.primary_job) from _call_set_duties_controller_promotion_after_convince
    if _return:
        $ the_person.set_event_day("work_duties_last_set")
        mc.name "I trust you can handle all of that?"
        "[the_person.possessive_title!c] nods."
        the_person "Yes [the_person.mc_title], I can."
        mc.name "That's what I like to hear."
    else:
        mc.name "I'll need to make some other decisions first. We'll talk about this soon, okay?"
        "[the_person.possessive_title!c] nods."

    return

label promotion_post_sex_convince(the_person):
    $ the_person.draw_person()
    $ ran_num = the_person.current_job.seniority_level + 1
    the_person "Now that that's taken care of..."
    "[the_person.title] looks at you expectantly."
    menu:
        "Promote her\n{menu_green}+1 Work Experience (To level [ran_num]){/menu_green}":
            mc.name "You've really proved your dedication [the_person.title]. You've earned this promotion."
            $ the_person.change_stats(love = 2, happiness = 10, slut = 2, max_slut = 50)
            call promotion_after_convince(the_person, for_sex = True) from _call_promotion_after_convince_1
            $ mc.stats.change_tracked_stat("Employee", "Promotions", 1)
            return True

        "Refuse":
            mc.name "Yeah... I still don't think you're ready for it."
            "She glares at you silently for a long moment."
            the_person "But... We just... We had a deal!"
            mc.name "Did we? I said I would consider it again. I considered it again, and I haven't changed my mind."
            mc.name "You want a promotion? Go and prove you're a good employee, not just an easy fucking slut."
            $ the_person.change_stats(love = -5, happiness = -10, obedience = -5)
            return False

        "Punish her for inappropriate behaviour" if office_punishment.is_active:
            mc.name "I'm sorry, but that wouldn't be appropriate for us to talk about now."
            mc.name "I already have to write you up for inappropriate behaviour in the workplace."
            "She laughs, but stops when she realises you aren't kidding."
            the_person "What? But... I only did that for you! You wanted it!"
            mc.name "Yeah, but rules are rules. That was clearly a violation of the company handbook. Here, I can show you if you want..."
            $ the_person.change_stats(love = -5, happiness = -10, obedience = -5, slut = -2, max_slut = 50)
            $ the_person.add_infraction(Infraction.inappropriate_behaviour_factory())
            return False
    return False #TODO Return True if you are going to promote her, False if you fucked her then turned her down

label employee_find_out_home_location_label(the_person):
    $ the_person.draw_person(position = "sitting")
    "You walk up to [the_person.possessive_title], who is sitting at her work station."

    mc.name "Hey [the_person.title], how long have you been working for me?"
    $ ran_num = the_person.current_job.days_employed // 7
    if ran_num == 0:
        the_person "I just started working here, is there something wrong?"
    elif ran_num == 1:
        the_person "It's about a week now, why do you ask?"
    else:
        the_person "It must be about [ran_num] weeks now, why do you ask?"

    mc.name "I don't think we ever had a personal talk, just to get to know each other."
    the_person "Oh, that's very nice of you, I would love to have a chat."
    $ the_person.change_happiness(5)

    mc.name "So tell me a little about yourself, [the_person.title]."

    $ ran_num = renpy.random.randint(2, ((the_person.age - 17) if (the_person.age - 17) > 0 else 1)  * 12)
    $ opinion = "months"
    if ran_num > 23:
        $ ran_num = ran_num // 12
        $ opinion = "years"

    if the_person.relationship == "Single":
        the_person "I'm still single, enjoying life and keeping things casual."
    elif the_person.relationship == "Girlfriend":
        the_person "My boyfriend is [the_person.SO_name] and we have been going out for about [ran_num] [opinion]."
    elif the_person.relationship == "Fiance":
        the_person "I'm engaged to my sweetheart [the_person.SO_name], we have been together for about [ran_num] [opinion]."
    elif the_person.relationship == "Married":
        the_person "I'm married and my husband's name is [the_person.SO_name], we have been married for about [ran_num] [opinion]."

    if the_person.kids == 0:
        if the_person.knows_pregnant:
            the_person "I've got no children, but that will change pretty soon."
        elif the_person.on_birth_control:
            the_person "I've got no children and to be honest I don't plan on getting any soon."
        else:
            the_person "I've got no children, but I wouldn't mind getting pregnant."
    elif the_person.kids == 1:
        if the_person.age < 35:
            the_person "I've an adorable little boy, a little cheeky, just like his dad."
        else:
            the_person "I've a wonderful daughter and I'm very proud of her, she has been a joy in my life."
    else:
        the_person "I have [the_person.kids] children and I love them all very much."

    mc.name "That sounds wonderful, [the_person.title], and where do you live?"

    if not the_person.has_significant_other and the_person.kids == 0:
        $ ran_num = renpy.random.randint(4, 8)
        $ opinion = get_random_from_list(["Peach Trees", "Nakatomi Plaza", "La Fortuna", "Villa Bonita", "St. Germaine"])
        the_person "I have a nice little place in the [opinion] apartment block on the [ran_num]th floor."
    else:
        $ opinion = get_random_from_list(["Lyon Estates just south of Hill Valley", "Bristol Avenue in Brentwood", "Carnarvon Park in Newbury", "Quimby Street in Cullen"])
        the_person "We have a beautiful home on [opinion]."

    "You just learned her home address and can visit her anytime you want."
    $ the_person.learn_home()

    mc.name "Well, well, that is indeed a great place to live. Thank you for the talk, I'm sorry to cut this short, but I do have to get back to work."
    $ rando = get_random_from_list(["talk", "parley", "chat", "babble", "conversation"])
    the_person "Any time [the_person.mc_title], I really enjoyed our little [rando]."

    $ clear_scene()
    $ jump_game_loop()
    return
