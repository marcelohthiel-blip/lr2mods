init 10 python:
    def update_outfit_color(item):
        if not item:
            return
        cs = renpy.current_screen()
        if cs.scope["selected_colour"] == "colour_pattern":
            item.colour_pattern = [cs.scope["current_r"], cs.scope["current_g"], cs.scope["current_b"], cs.scope["current_a"]]
            if item.has_extension:
                item.has_extension.colour_pattern = [cs.scope["current_r"], cs.scope["current_g"], cs.scope["current_b"], cs.scope["current_a"]]
        else:
            item.colour = [cs.scope["current_r"], cs.scope["current_g"], cs.scope["current_b"], cs.scope["current_a"]]
            if item.has_extension:
                item.has_extension.colour = [cs.scope["current_r"], cs.scope["current_g"], cs.scope["current_b"], cs.scope["current_a"]]

    def update_color_selection(item):
        if not item:
            return
        cs = renpy.current_screen()
        if cs.scope["selected_colour"] == "colour_pattern":
            cs.scope["current_r"] = item.colour_pattern[0]
            cs.scope["current_g"] = item.colour_pattern[1]
            cs.scope["current_b"] = item.colour_pattern[2]
            cs.scope["current_a"] = item.colour_pattern[3]
        else:
            cs.scope["current_r"] = item.colour[0]
            cs.scope["current_g"] = item.colour[1]
            cs.scope["current_b"] = item.colour[2]
            cs.scope["current_a"] = item.colour[3]

    def hide_mannequin():
        cs = renpy.current_screen()
        # release display resources
        if "mannequin" in cs.scope.keys() and isinstance(cs.scope["mannequin"], Person):
            renpy.hide(str(cs.scope["mannequin"].identifier), layer = "mannequin")
        else:
            renpy.hide("mannequin_dummy", layer = "mannequin")
        return

    def preview_outfit(outfit = "demo_outfit"):
        cs = renpy.current_screen()
        hide_list = []
        if cs.scope["hide_underwear"]:
            hide_list.append(0)
            hide_list.append(1)
        if cs.scope["hide_shoes"]:
            hide_list.append(2)
        if cs.scope["hide_base"]:
            hide_list.append(3)
        if cs.scope["hide_overwear"]:
            hide_list.append(4)

        cs.scope["hide_list"] = hide_list
        if cs.scope["mannequin"] == "mannequin":
            draw_average_mannequin(cs.scope[outfit], hide_list = hide_list)
        else:
            draw_mannequin(cs.scope["mannequin"], cs.scope[outfit], cs.scope["mannequin_pose"], hide_list = hide_list)

    def preview_clothing(apply_method, cloth, outfit = "demo_outfit"):
        cs = renpy.current_screen()

        cloth.colour[0] = cs.scope["current_r"]
        cloth.colour[1] = cs.scope["current_g"]
        cloth.colour[2] = cs.scope["current_b"]
        cloth.colour[3] = cs.scope["current_a"]

        apply_method(cs.scope[outfit], cloth)

    def hide_preview(cloth, outfit = "demo_outfit"):
        cs = renpy.current_screen()
        cs.scope[outfit].remove_clothing(cloth)

    def get_slut_score():
        cs = renpy.current_screen()

        if cs.scope["outfit_type"] == "full":
            return cs.scope["demo_outfit"].outfit_slut_score
        elif cs.scope["outfit_type"] == "under":
            return cs.scope["demo_outfit"].underwear_slut_score
        elif cs.scope["outfit_type"] == "over":
            return cs.scope["demo_outfit"].overwear_slut_score
        return 0

    def get_outfit_type_name():
        cs = renpy.current_screen()

        if cs.scope["outfit_type"] == "full":
            return "Full Outfit"
        if cs.scope["outfit_type"] == "under":
            return "Underwear"
        if cs.scope["outfit_type"] == "over":
            return "Overwear"
        return "Unknown"

    def get_category(item):
        cs = renpy.current_screen()

        for cat in cs.scope["valid_categories"]:
            for cloth in cs.scope["categories_mapping"][cat][0]:
                if item.name == cloth.name:
                    return cat
        return cs.scope["valid_categories"][0]

    def set_generated_outfit(outfit_type, slut_value, min_slut_value = 0):
        cs = renpy.current_screen()
        outfit = cs.scope["outfit_builder"].build_outfit(outfit_type, slut_value, min_slut_value)
        outfit.update_name()
        cs.scope["demo_outfit"] = outfit
        cs.scope["base_outfit"] = outfit.get_copy()
        cs.scope["selected_clothing"] = None
        preview_outfit()

    def personalize_generated_outfit():
        cs = renpy.current_screen()
        cs.scope["outfit_builder"].personalize_outfit(cs.scope["demo_outfit"], swap_bottoms = False, allow_skimpy = False)
        cs.scope["demo_outfit"].update_name()
        cs.scope["base_outfit"] = cs.scope["demo_outfit"].get_copy()
        preview_outfit()

    def switch_to_mannequin(person):
        cs = renpy.current_screen()
        if person:
            cs.scope["mannequin"] = person
            cs.scope["outfit_builder"] = WardrobeBuilder(person)
        else:
            cs.scope["mannequin"] = "mannequin"
            cs.scope["outfit_builder"] = WardrobeBuilder(None)

    def update_outfit_name(outfit):
        default_names = ["New Outfit", "New Overwear Set", "New Underwear Set"]
        if outfit.name in default_names or outfit.name == "":
            outfit.update_name()

    def colour_changed_bar(new_value): # Handles the changes to clothing colors, both normal and with patterns. Covers all channels.
        if not new_value:
            new_value = 0
        try:
            new_value = float(new_value)
        except ValueError:
            new_value = 0
        if float(new_value) < 0:
            new_value = 0
        elif float(new_value) > 1:
            new_value = 1.0

        cs = renpy.current_screen()
        bar_value = cs.scope["bar_value"]

        if bar_value == "current_a":
            cs.scope["current_a"] = builtins.round(float(new_value),2)
        if bar_value == "current_r":
            cs.scope["current_r"] = builtins.round(float(new_value),2)
        if bar_value == "current_g":
            cs.scope["current_g"] = builtins.round(float(new_value),2)
        if bar_value == "current_b":
            cs.scope["current_b"] = builtins.round(float(new_value),2)

        update_outfit_color(cs.scope["selected_clothing"])
        preview_outfit()
        renpy.restart_interaction()

    def update_transparency_bar(new_value):
        update_transparency(new_value + .33)

    def update_transparency(new_value):
        cs = renpy.current_screen()

        if not new_value:
            new_value = .95

        try:
            new_value = float(new_value)
        except ValueError:
            new_value = .95

        if new_value < .33:
            new_value = .33
        elif new_value > 1.0:
            new_value = 1.0

        cs.scope["current_a"] = builtins.round(float(new_value), 2)

        update_outfit_color(cs.scope["selected_clothing"])
        preview_outfit()
        renpy.restart_interaction()

    def change_color_luminance(value):
        cs = renpy.current_screen()

        colour = WardrobeBuilder.apply_variation([cs.scope["current_r"], cs.scope["current_g"], cs.scope["current_b"], cs.scope["current_a"]], value, True)

        cs.scope["current_r"] = colour[0]
        cs.scope["current_g"] = colour[1]
        cs.scope["current_b"] = colour[2]
        cs.scope["current_a"] = colour[3]

        update_outfit_color(cs.scope["selected_clothing"])
        preview_outfit()
        renpy.restart_interaction()

    def update_slut_generation(new_value):
        cs = renpy.current_screen()

        if new_value < cs.scope["min_slut_generation"]:
            new_value = cs.scope["min_slut_generation"]
        if new_value > 8:
            new_value = 8

        cs.scope["slut_generation"] = new_value
        renpy.restart_interaction()

    def update_min_slut_generation(new_value):
        cs = renpy.current_screen()

        if new_value < 0:
            new_value = 0
        if new_value > 4:
            new_value = 4
        elif new_value > cs.scope["slut_generation"]:
            new_value = cs.scope["slut_generation"]

        cs.scope["min_slut_generation"] = new_value
        renpy.restart_interaction()

    def generate_stat_slug(clothing): #Generates a string of text/tokens representing what layer this clothing item is/covers
        cloth_info = ""
        if clothing.layer == 4:
            cloth_info += "{image=gui/extra_images/overwear_token.png}"
        if clothing.layer == 3:
            if clothing in neckwear_list:
                cloth_info += "{image=gui/extra_images/necklace_token.png}"
            else:
                cloth_info += "{image=gui/extra_images/clothing_token.png}"
        if clothing.layer == 2:
            if clothing in shoes_list:
                cloth_info += "{image=gui/extra_images/shoes_token.png}"
            elif clothing in (light_eye_shadow, heavy_eye_shadow, blush, lipstick):
                cloth_info += "{image=gui/extra_images/makeup_token.png}"
            elif clothing in neckwear_list:
                cloth_info += "{image=gui/extra_images/necklace_token.png}"
            elif clothing in tights_list:
                cloth_info += "{image=gui/extra_images/stocking_token.png}"
            else:
                cloth_info += "{image=gui/extra_images/accessory_token.png}"
        if clothing.layer == 1:
            if clothing in socks_list:
                cloth_info += "{image=gui/extra_images/stocking_token.png}"
            else:
                cloth_info += "{image=gui/extra_images/underwear_token.png}"
        if clothing.layer == 0:
            cloth_info += "{image=gui/extra_images/sexy_underwear_token.png}"

        if clothing.has_extension: #Display a second token if the clothing item is a different part (split coverage into top and bottom?)
            if clothing.has_extension.layer == 4:
                cloth_info += "|{image=gui/extra_images/overwear_token.png}"
            if clothing.has_extension.layer == 3:
                if clothing.has_extension in [neckwear_list]:
                    cloth_info += "|{image=gui/extra_images/accessory_token.png}"
                else:
                    cloth_info += "|{image=gui/extra_images/clothing_token.png}"
            if clothing.has_extension.layer == 2:
                if clothing.has_extension in shoes_list:
                    cloth_info += "|{image=gui/extra_images/shoes_token.png}"
                else:
                    cloth_info += "|{image=gui/extra_images/accessory_token.png}"
            if clothing.has_extension.layer == 1:
                cloth_info += "|{image=gui/extra_images/underwear_token.png}"
            if clothing.has_extension.layer == 0:
                cloth_info += "|{image=gui/extra_images/sexy_underwear_token.png}"

        cloth_info += f" +{clothing.slut_score} {{image=gold_heart_token_small}}"
        return cloth_info

    def get_xml_files_from_path():
        result = []
        for file in renpy.list_files():
            if file.endswith(".xml"):
                base = os.path.basename(file)
                result.append(os.path.splitext(base)[0])
        result.sort()
        return result

screen outfit_creator(start_outfit, outfit_type = "full", slut_limit = None, target_wardrobe = mc.designed_wardrobe, start_mannequin = "mannequin", outfit_validator = None): ##Pass a completely blank outfit instance for a new outfit, or an already existing instance to load an old one.| This overrides the default outfit creation screen
    add paper_background_image
    modal True

    frame:
        background "#88888808"
        xpos 1460
        ypos 0
        xysize (460, 1080)

    default fluids_list = [face_cum.get_copy(), mouth_cum.get_copy(), stomach_cum.get_copy(), tits_cum.get_copy(), ass_cum.get_copy(), creampie_cum.get_copy()]

    default mannequin = start_mannequin
    default mannequin_pose = "stand3" if mannequin == "mannequin" else mannequin.idle_pose
    default mannequin_selection = False
    default mannequin_poser = False

    default selected_xml = "Exported_Wardrobe.xml"
    default transparency_selection = True
    default outfit_stats = True
    default outfit_class_selected = None
    default color_selection = True
    default import_selection = False
    default transparency_slider = False

    default demo_outfit = start_outfit.get_copy()
    default base_outfit = start_outfit
    default outfit_builder = WardrobeBuilder(None if mannequin == "mannequin" else mannequin)
    default max_slut = 8
    default hide_underwear = False
    default hide_shoes = False
    default hide_base = False
    default hide_overwear = False
    default hide_list = []

    on "show":
        if mannequin == "mannequin":
            action Function(draw_average_mannequin, base_outfit, hide_list = [])
        elif isinstance(mannequin, Person):
            action Function(draw_mannequin, mannequin, base_outfit, mannequin.idle_pose, hide_list = [])

    if outfit_type == "makeup":
        default categories_mapping = {
            "Makeup": [makeup_list, Outfit.can_add_accessory, Outfit.add_accessory],
            "Facial": [[x for x in earings_list if x not in makeup_list], Outfit.can_add_accessory, Outfit.add_accessory],
            "Rings": [rings_list, Outfit.can_add_accessory, Outfit.add_accessory],
            "Bracelets": [bracelet_list, Outfit.can_add_accessory, Outfit.add_accessory],
            "Neckwear": [neckwear_list, Outfit.can_add_accessory, Outfit.add_accessory]
        }
        $ valid_layers = [2]
    if outfit_type == "under":
        default categories_mapping = {
            "Panties": [panties_list, Outfit.can_add_lower, Outfit.add_lower],  #Maps each category to the function it should use to determine if it is valid and how it should be added to the outfit.
            "Bras": [real_bra_list + [cincher, heart_pasties], Outfit.can_add_upper, Outfit.add_upper],
            "One Piece": [one_piece_list, Outfit.can_add_dress, Outfit.add_dress],
            "Socks": [socks_list, Outfit.can_add_feet, Outfit.add_feet],
            "Shoes": [shoes_list, Outfit.can_add_feet, Outfit.add_feet],
            "Makeup": [makeup_list, Outfit.can_add_accessory, Outfit.add_accessory],
            "Facial": [[x for x in earings_list if x not in makeup_list], Outfit.can_add_accessory, Outfit.add_accessory],
            "Rings": [rings_list, Outfit.can_add_accessory, Outfit.add_accessory],
            "Bracelets": [bracelet_list, Outfit.can_add_accessory, Outfit.add_accessory],
            "Neckwear": [neckwear_list, Outfit.can_add_accessory, Outfit.add_accessory],
            "Not Paint": [fluids_list, Outfit.can_add_accessory, Outfit.add_accessory]
        }
        $ valid_layers = [0,1,2]
        $ outfit_class_selected = "Underwear"
    elif outfit_type == "over":
        default categories_mapping = {
            "One Piece": [one_piece_list, Outfit.can_add_dress, Outfit.add_dress],
            "Nightgowns": [nightgown_list, Outfit.can_add_dress, Outfit.add_dress],
            "Pants": [real_pants_list, Outfit.can_add_lower, Outfit.add_lower],
            "Skirts": [skirts_list, Outfit.can_add_lower, Outfit.add_lower],
            "Dresses": [real_dress_list, Outfit.can_add_dress, Outfit.add_dress],
            "Shirts": [real_shirt_list, Outfit.can_add_upper, Outfit.add_upper],
            "Outer wear": [outerwear_list, Outfit.can_add_upper, Outfit.add_upper],
            "Tights": [tights_list, Outfit.can_add_lower, Outfit.add_lower],
            "Shoes": [shoes_list, Outfit.can_add_feet, Outfit.add_feet],
            "Makeup": [makeup_list, Outfit.can_add_accessory, Outfit.add_accessory],
            "Facial": [[x for x in earings_list if x not in makeup_list], Outfit.can_add_accessory, Outfit.add_accessory],
            "Rings": [rings_list, Outfit.can_add_accessory, Outfit.add_accessory],
            "Bracelets": [bracelet_list, Outfit.can_add_accessory, Outfit.add_accessory],
            "Neckwear": [neckwear_list, Outfit.can_add_accessory, Outfit.add_accessory]}
        $ valid_layers = [2,3,4]
        $ outfit_class_selected = "Overwear"
    else:
        default categories_mapping = {
            "Panties": [panties_list, Outfit.can_add_lower, Outfit.add_lower],  #Maps each category to the function it should use to determine if it is valid and how it should be added to the outfit.
            "Bras": [real_bra_list + [cincher, heart_pasties], Outfit.can_add_upper, Outfit.add_upper],
            "One Piece": [one_piece_list, Outfit.can_add_dress, Outfit.add_dress],
            "Nightgowns": [nightgown_list, Outfit.can_add_dress, Outfit.add_dress],
            "Pants": [real_pants_list, Outfit.can_add_lower, Outfit.add_lower],
            "Skirts": [skirts_list, Outfit.can_add_lower, Outfit.add_lower],
            "Dresses": [real_dress_list, Outfit.can_add_dress, Outfit.add_dress],
            "Shirts": [real_shirt_list, Outfit.can_add_upper, Outfit.add_upper],
            "Outer wear": [outerwear_list, Outfit.can_add_upper, Outfit.add_upper],
            "Socks": [socks_list, Outfit.can_add_feet, Outfit.add_feet],
            "Tights": [tights_list, Outfit.can_add_lower, Outfit.add_lower],
            "Shoes": [shoes_list, Outfit.can_add_feet, Outfit.add_feet],
            "Makeup": [makeup_list, Outfit.can_add_accessory, Outfit.add_accessory],
            "Facial": [[x for x in earings_list if x not in makeup_list], Outfit.can_add_accessory, Outfit.add_accessory],
            "Rings": [rings_list, Outfit.can_add_accessory, Outfit.add_accessory],
            "Bracelets": [bracelet_list, Outfit.can_add_accessory, Outfit.add_accessory],
            "Neckwear": [neckwear_list, Outfit.can_add_accessory, Outfit.add_accessory],
            "Not Paint": [fluids_list, Outfit.can_add_accessory, Outfit.add_accessory]}
        $ valid_layers = [0,1,2,3,4]
        $ outfit_class_selected = "Outfit"

    default valid_categories = list(categories_mapping.keys())
    default category_selected = next(iter(categories_mapping))
    default gold_heart = Composite((24, 24), (0, 1), Image(get_file_handle("gold_heart.png")))

    default bar_select = 0 # 0 is nothing selected, 1 is red, 2 is green, 3 is blue, and 4 is alpha
    default bar_value = None # Stores information about which bar is being changed and is then passed to colour_changed_bar() as default value

    default selected_clothing = None
    default selected_clothing_colour = None
    default selected_colour = "colour" #If secondary we are altering the pattern colour. When changed it updates the colour of the clothing item. Current values are "colour" and "colour_pattern"

    default current_r = 1.0
    default current_g = 1.0
    default current_b = 1.0
    default current_a = 1.0

    default slut_generation = 0
    default min_slut_generation = 0

    # $ current_colour = [1.0,1.0,1.0,1.0] #This is the colour we will apply to all of the clothing

    #Each category below has a click to enable button. If it's false, we don't show anything for it.
    #TODO: refactor this outfit creator to remove as much duplication as possible.


    hbox: #The main divider between the new item adder and the current outfit view.
        xpos 15
        yalign 0.5
        yanchor 0.5
        spacing 15
        frame:
            background "#0a142688"
            padding (20,20)
            xysize (880, 1015)
            hbox:
                spacing 15
                frame:
                    background "#0a142688"
                    xsize 200

                    viewport:
                        mousewheel True
                        draggable True
                        grid 1 builtins.len(valid_categories): #categories select on far left
                            for category in valid_categories:
                                textbutton "[category]":
                                    style "textbutton_style"
                                    text_style "serum_text_style"

                                    xfill True
                                    background ("#171717" if category == category_selected else "#143869")
                                    hover_background "#1a45a1"
                                    sensitive category != category_selected

                                    action [
                                        SetScreenVariable("category_selected", category),
                                        SetScreenVariable("selected_clothing", None),
                                        SetScreenVariable("selected_colour", "colour")
                                    ]
                vbox:
                    spacing 5
                    viewport:
                        ysize 520
                        xminimum 605
                        scrollbars "vertical"
                        mousewheel True
                        frame:
                            xsize 605
                            yminimum 520
                            background "#0a142688"
                            padding 0,0

                            vbox:
                                #THIS IS WHERE ITEM CHOICES ARE SHOWN
                                if category_selected in categories_mapping:
                                    $ valid_check = categories_mapping[category_selected][1]
                                    $ apply_method = categories_mapping[category_selected][2]
                                    $ cloth_list_length = builtins.len(categories_mapping[category_selected][0])

                                    for cloth in sorted(categories_mapping[category_selected][0], key = lambda x: (x.layer, x.slut_score, x.name)):
                                        python:
                                            name = cloth.name.title()
                                            stat_slut = generate_stat_slug(cloth)
                                            is_sensitive = valid_check(base_outfit, cloth) and any(x in cloth.layers for x in valid_layers)

                                        frame:
                                            xsize 605
                                            ysize 50
                                            background None
                                            padding 0,0

                                            textbutton "[name]":
                                                xalign 0.0
                                                ysize 50
                                                text_align .5
                                                xfill True
                                                style "textbutton_style"
                                                text_style "custom_outfit_style"

                                                if is_sensitive:
                                                    background "#143869"
                                                    hover_background "#1a45a1"
                                                else:
                                                    background "#143869"
                                                    hover_background "#1a45a1"
                                                insensitive_background "#171717"
                                                sensitive is_sensitive
                                                action [
                                                    SetScreenVariable("selected_clothing", cloth.get_copy()),
                                                    SetScreenVariable("selected_colour", "colour")
                                                ]
                                                hovered [
                                                    Function(preview_clothing, apply_method, cloth),
                                                    Function(preview_outfit)
                                                ]
                                                unhovered [
                                                    Function(hide_preview, cloth),
                                                    Function(preview_outfit)
                                                ]


                                            text "[stat_slut]":
                                                style "custom_outfit_style"
                                                ysize 50
                                                xalign .95
                                                yalign 1
                                                yoffset 10
                    frame:
                        #THIS IS WHERE SELECTED ITEM OPTIONS ARE SHOWN
                        xysize (645, 440)
                        background "#0a142688"
                        if selected_clothing is not None:
                            $ selected_stat_slug = generate_stat_slug(selected_clothing)
                            vbox:
                                text "[selected_clothing.name] [selected_stat_slug]" style "serum_text_style_header"

                                frame:
                                    background "#0a142688"
                                    yfill True
                                    xfill True
                                    viewport:
                                        xsize 605
                                        draggable True
                                        mousewheel True
                                        yfill True
                                        vbox:
                                            spacing 5
                                            vbox:
                                                spacing 5
                                                hbox:
                                                    spacing 5
                                                    if len(selected_clothing.supported_patterns) > 1:
                                                        frame:
                                                            background "#0a142688"
                                                            ysize 40
                                                            viewport:
                                                                mousewheel "horizontal"
                                                                draggable True

                                                                grid builtins.len(selected_clothing.supported_patterns) 1:
                                                                    xfill True
                                                                    for pattern in selected_clothing.supported_patterns:

                                                                        textbutton "[pattern]":
                                                                            style "textbutton_no_padding_highlight"
                                                                            text_style "serum_text_style"
                                                                            xalign 0.5
                                                                            xfill True
                                                                            ysize 40

                                                                            if selected_clothing.pattern == selected_clothing.supported_patterns[pattern]:
                                                                                hover_background "#143869"
                                                                                background "#14386988"
                                                                            else:
                                                                                hover_background "#143869"
                                                                                background "#171717"

                                                                            sensitive True
                                                                            action [
                                                                                SetField(selected_clothing,"pattern",selected_clothing.supported_patterns[pattern]),
                                                                                Function(preview_outfit)
                                                                            ]

                                                hbox:
                                                    xfill True
                                                    spacing 5 #We will manually handle spacing so we can have our colour predictor frames
                                                    frame:
                                                        ysize 40
                                                        background "#0a142688"
                                                        hbox:
                                                            spacing 5
                                                            textbutton "Primary":
                                                                style "textbutton_no_padding_highlight"
                                                                text_style "serum_text_style"

                                                                if selected_colour == "colour":
                                                                    hover_background "#143869"
                                                                    background "#14386988"
                                                                else:
                                                                    hover_background "#143869"
                                                                    background "#171717"
                                                                sensitive True
                                                                if selected_colour == "colour_pattern":
                                                                    action [
                                                                        SetField(selected_clothing,"colour_pattern",[current_r,current_g,current_b,current_a]),
                                                                        SetScreenVariable("selected_colour","colour"),
                                                                        SetScreenVariable("current_r",selected_clothing.colour[0]),
                                                                        SetScreenVariable("current_g",selected_clothing.colour[1]),
                                                                        SetScreenVariable("current_b",selected_clothing.colour[2]),
                                                                        SetScreenVariable("current_a",selected_clothing.colour[3])
                                                                    ]
                                                                else:
                                                                    action ToggleScreenVariable("color_selection")

                                                            frame:
                                                                if selected_colour == "colour":
                                                                    background Color(rgb=(current_r,current_g,current_b), alpha = current_a)
                                                                else:
                                                                    background Color(rgb=(selected_clothing.colour[0], selected_clothing.colour[1], selected_clothing.colour[2]))
                                                                yfill True
                                                                xsize 50


                                                            if builtins.type(selected_clothing) is Clothing and selected_clothing.pattern is not None:
                                                                textbutton "Pattern":
                                                                    style "textbutton_no_padding_highlight"
                                                                    text_style "serum_text_style"

                                                                    if selected_colour == "colour_pattern":
                                                                        hover_background "#143869"
                                                                        background "#14386988"
                                                                    else:
                                                                        hover_background "#143869"
                                                                        background "#171717"
                                                                    sensitive True
                                                                    if selected_colour == "colour":
                                                                        action [
                                                                            SetField(selected_clothing,"colour",[current_r,current_g,current_b,current_a]),
                                                                            SetScreenVariable("selected_colour","colour_pattern"),
                                                                            SetScreenVariable("current_r",selected_clothing.colour_pattern[0]),
                                                                            SetScreenVariable("current_g",selected_clothing.colour_pattern[1]),
                                                                            SetScreenVariable("current_b",selected_clothing.colour_pattern[2]),
                                                                            SetScreenVariable("current_a",selected_clothing.colour_pattern[3])
                                                                        ]
                                                                    else:
                                                                        action ToggleScreenVariable("color_selection")
                                                                frame:
                                                                    if selected_colour == "colour_pattern":
                                                                        background Color(rgb=(current_r,current_g,current_b), alpha = current_a)
                                                                    else:
                                                                        background Color(rgb=(selected_clothing.colour_pattern[0], selected_clothing.colour_pattern[1], selected_clothing.colour_pattern[2]))
                                                                    yfill True
                                                                    xsize 50

                                                            textbutton "Lighten":
                                                                style "textbutton_no_padding_highlight"
                                                                text_style "serum_text_style"
                                                                hover_background "#143869"
                                                                background "#14386988"
                                                                action Function(change_color_luminance, 2)

                                                            textbutton "Darken":
                                                                style "textbutton_no_padding_highlight"
                                                                text_style "serum_text_style"
                                                                hover_background "#143869"
                                                                background "#14386988"
                                                                action Function(change_color_luminance, -2)

                                            vbox:
                                                spacing 5
                                                hbox:
                                                    spacing 5
                                                    if color_selection:
                                                        vbox:
                                                            spacing 5
                                                            grid 3 1:
                                                                xfill True
                                                                frame:

                                                                    background "#0a142688"
                                                                    hbox:
                                                                        button:
                                                                            background "#dd1f1f"
                                                                            action ToggleScreenVariable("bar_select", 1, 0)
                                                                            hovered SetScreenVariable("bar_value", "current_r")

                                                                            if bar_select == 1:
                                                                                input default current_r length 4 changed colour_changed_bar allow ".0123456789" style "serum_text_style" size 16
                                                                            else:
                                                                                text "R [current_r:.2f]" style "serum_text_style" yalign 0.5 size 16
                                                                            xsize 75
                                                                            ysize 45
                                                                        bar:
                                                                            adjustment ui.adjustment(range = 1.00, value = current_r, step = 0.1, changed = colour_changed_bar)
                                                                            xfill True
                                                                            ysize 45
                                                                            style style.slider

                                                                            hovered SetScreenVariable("bar_value", "current_r")
                                                                            unhovered [SetScreenVariable("current_r",builtins.round(current_r,2))]

                                                                frame:

                                                                    background "#0a142688"
                                                                    hbox:
                                                                        button:
                                                                            background "#3ffc45"
                                                                            action ToggleScreenVariable("bar_select", 2, 0)
                                                                            hovered SetScreenVariable("bar_value", "current_g")

                                                                            if bar_select == 2:
                                                                                input default current_g length 4 changed colour_changed_bar allow ".0123456789" style "serum_text_style" size 16
                                                                            else:
                                                                                text "G [current_g:.2f]" style "serum_text_style" yalign 0.5 size 16
                                                                            xsize 75
                                                                            ysize 45
                                                                        bar:
                                                                            adjustment ui.adjustment(range = 1.00, value = current_g, step = 0.1, changed = colour_changed_bar)
                                                                            xfill True
                                                                            ysize 45
                                                                            style style.slider

                                                                            hovered SetScreenVariable("bar_value", "current_g")
                                                                            unhovered [SetScreenVariable("current_g",builtins.round(current_g,2))]
                                                                frame:

                                                                    background "#0a142688"
                                                                    hbox:
                                                                        button:
                                                                            background "#3f87fc"
                                                                            action ToggleScreenVariable("bar_select", 3, 0)
                                                                            hovered SetScreenVariable("bar_value", "current_b")

                                                                            if bar_select == 3:
                                                                                input default current_b length 4 changed colour_changed_bar allow ".0123456789" style "serum_text_style" size 16
                                                                            else:
                                                                                text "B [current_b:.2f]" style "serum_text_style" yalign 0.5 size 16

                                                                            xsize 75
                                                                            ysize 45
                                                                        bar:
                                                                            adjustment ui.adjustment(range = 1.00, value = current_b, step = 0.1, changed = colour_changed_bar)
                                                                            xfill True
                                                                            ysize 45
                                                                            style style.slider

                                                                            hovered SetScreenVariable("bar_value", "current_b")
                                                                            unhovered [SetScreenVariable("current_b",builtins.round(current_b,2))]

                                                            # frame:
                                                            #     background "#0a142688"
                                                            #     hbox:
                                                            #         button:
                                                            #             background "#111111"
                                                            #             action ToggleScreenVariable("bar_select", 4, 0)
                                                            #             hovered SetScreenVariable("bar_value", "current_a")

                                                            #             if bar_select == 4:
                                                            #                 input default current_a length 4 changed colour_changed_bar allow ".0123456789" style "serum_text_style" size 16
                                                            #             else:
                                                            #                 text "A "+ "%.2f" % current_a style "serum_text_style" yalign 0.5 size 16
                                                            #             xsize 75
                                                            #             ysize 45

                                                            hbox:
                                                                spacing 5
                                                                if transparency_slider:
                                                                    $ trans_name = str(int(float(current_a)*100)) + "%"
                                                                    frame:
                                                                        background "#0a142688"
                                                                        hbox:
                                                                            button:
                                                                                background "#666666"
                                                                                text "T: [trans_name]" style "serum_text_style" yalign 0.5 size 16
                                                                                xysize (75, 40)
                                                                                action ToggleScreenVariable("transparency_slider", True, False)

                                                                            bar:
                                                                                adjustment ui.adjustment(range = 0.67, value = current_a - .33, step = 0.01, changed = update_transparency_bar)
                                                                                xfill True
                                                                                style style.slider
                                                                                hovered SetScreenVariable("bar_value", "current_b")
                                                                                unhovered [SetScreenVariable("current_a",builtins.round(current_a, 2))]
                                                                                ysize 40

                                                                else:
                                                                    for trans in ('1.0', '0.95', '0.9', '0.8', '0.75', '0.66', '0.5', '0.33'):
                                                                        $ trans_name = str(int(float(trans)*100)) + "%"
                                                                        button:
                                                                            if current_a == float(trans):
                                                                                hover_background "#143869"
                                                                                background "#14386988"
                                                                            else:
                                                                                hover_background "#143869"
                                                                                background "#171717"
                                                                            text "[trans_name]" style "menu_text_style" xalign 0.5 xanchor 0.5 yalign 0.5 yanchor 0.5
                                                                            xysize (60, 40)
                                                                            action [Function(update_transparency, float(trans))]
                                                                button:
                                                                    action ToggleScreenVariable("transparency_slider", True, False)
                                                                    text str(int(float(current_a)*100)) + "%" style "serum_text_style" yalign 0.5 size 16
                                                                    padding (0,0)
                                                                    xysize (60, 40)
                                                                    background "#143869"

                                                if color_selection:
                                                    for block_count, colour_list in builtins.enumerate(split_list_in_blocks(persistent.colour_palette, 13)):
                                                        hbox:
                                                            spacing 0
                                                            yanchor (block_count * .1)

                                                            for count, a_colour in builtins.enumerate(colour_list):
                                                                frame:
                                                                    background "#0a142688"
                                                                    padding (3, 3)
                                                                    button:
                                                                        if a_colour[0] == 1 and a_colour[1] == 1 and a_colour[2] == 1 and a_colour[3] == 1:
                                                                            background Color(rgb = (.2, .2, .2), alpha = 1)
                                                                        else:
                                                                            background Color(rgb=(a_colour[0], a_colour[1], a_colour[2]))
                                                                        xysize (38, 38)
                                                                        sensitive True
                                                                        xalign True
                                                                        yalign True
                                                                        action [
                                                                            SetScreenVariable("current_r", a_colour[0]),
                                                                            SetScreenVariable("current_g", a_colour[1]),
                                                                            SetScreenVariable("current_b", a_colour[2]),
                                                                            SetScreenVariable("current_a", a_colour[3]),
                                                                            Function(update_outfit_color, selected_clothing),
                                                                            Function(preview_outfit)
                                                                        ]
                                                                        alternate [
                                                                            Function(update_colour_palette, count + (block_count * 13), current_r, current_g, current_b, current_a)
                                                                        ]

                                frame:
                                    background "#0a142688"
                                    xfill True
                                    textbutton "Add [selected_clothing.name]":
                                        style "textbutton_no_padding_highlight"
                                        text_style "serum_text_style"
                                        hover_background "#143869"
                                        background "#0a142688"
                                        insensitive_background"#171717"
                                        xalign 0.5
                                        xfill True

                                        sensitive valid_check(base_outfit, selected_clothing)

                                        action [
                                            SetField(selected_clothing, selected_colour,[current_r,current_g,current_b,current_a]),
                                            Function(apply_method, base_outfit, selected_clothing)
                                        ]
                                        hovered [
                                            SetField(selected_clothing, selected_colour,[current_r,current_g,current_b,current_a]),
                                            Function(apply_method, demo_outfit, selected_clothing),
                                            Function(preview_outfit)
                                        ]
                                        unhovered [
                                            Function(demo_outfit.remove_clothing, selected_clothing),
                                            Function(preview_outfit)
                                        ]

        vbox:
            spacing 15
            frame:
                xysize (540, 500)
                background "#0a142688"
                vbox:
                    spacing 5
                    frame:
                        background "#0a142688"
                        xfill True
                        ysize 60
                        textbutton "[demo_outfit.name]":
                            style "textbutton_no_padding_highlight"
                            text_style "serum_text_style"
                            xfill True
                            ysize 60
                            sensitive outfit_type != "makeup"
                            action [
                                Function(demo_outfit.update_name)
                            ]
                            tooltip "Current outfit name. Click to generate new name based on current clothing."

                    vbox:
                        grid 2 1:
                            xfill True
                            spacing 5
                            frame:
                                background "#0a142688"
                                xfill True
                                textbutton "View Outfit Stats":
                                    style "textbutton_no_padding_highlight"
                                    text_style "serum_text_style"
                                    xfill True

                                    action ToggleScreenVariable("outfit_stats")
                            frame:
                                background "#0a142688"
                                xfill True
                                textbutton f"Current ({Outfit.classification(get_slut_score())})":
                                    style "textbutton_no_padding"
                                    text_style "serum_text_style"
                                    xfill True

                        hbox:
                            spacing 5
                            vbox:
                                xalign 0.5
                                if outfit_stats:
                                    frame:
                                        background "#0a142688"
                                        ysize 314
                                        viewport:
                                            draggable True
                                            mousewheel True
                                            yfill True
                                            xsize 250
                                            vbox:
                                                frame:
                                                    background "#143869"
                                                    xsize 250
                                                    padding (1,1)
                                                    text f"Sluttiness ({get_outfit_type_name()}): {get_slut_score()}%" style "serum_text_style_traits"
                                                frame:
                                                    background "#143869"
                                                    xsize 250
                                                    padding (1,1)
                                                    text "Tits Visible: [demo_outfit.tits_visible]" style "serum_text_style_traits"
                                                frame:
                                                    background "#143869"
                                                    xsize 250
                                                    padding (1,1)
                                                    text "Tits Usable: [demo_outfit.tits_available]" style "serum_text_style_traits"
                                                frame:
                                                    background "#143869"
                                                    xsize 250
                                                    padding (1,1)
                                                    text "Wearing a Bra: [demo_outfit.wearing_bra]" style "serum_text_style_traits"
                                                frame:
                                                    background "#143869"
                                                    xsize 250
                                                    padding (1,1)
                                                    text "Bra Covered: [demo_outfit.bra_covered]" style "serum_text_style_traits"
                                                frame:
                                                    background "#143869"
                                                    xsize 250
                                                    padding (1,1)
                                                    text "Pussy Visible: [demo_outfit.vagina_visible]" style "serum_text_style_traits"
                                                frame:
                                                    background "#143869"
                                                    xsize 250
                                                    padding (1,1)
                                                    text "Pussy Usable: [demo_outfit.vagina_available]" style "serum_text_style_traits"
                                                frame:
                                                    background "#143869"
                                                    xsize 250
                                                    padding (1,1)
                                                    text "Wearing Panties: [demo_outfit.wearing_panties]" style "serum_text_style_traits"
                                                frame:
                                                    background "#143869"
                                                    xsize 250
                                                    padding (1,1)
                                                    text "Panties Covered: [demo_outfit.panties_covered]" style "serum_text_style_traits"
                                                frame:
                                                    background "#143869"
                                                    xsize 250
                                                    padding (1,1)
                                                    text "Legal in Public: [demo_outfit.is_legal_in_public]" style "serum_text_style_traits"

                                                # DEBUG CODE TO SEE THE BODY SCORE AND SLUT SCORE OF THE OUTFIT
                                                # frame:
                                                #     background "#143869"
                                                #     xsize 250
                                                #     padding (1,1)
                                                #     text "Body score: " + str(demo_outfit._m1_Outfit__get_body_parts_slut_score(outfit_type = outfit_type)) style "serum_text_style_traits"
                                                # frame:
                                                #     background "#143869"
                                                #     xsize 250
                                                #     padding (1,1)
                                                #     text "Slut score: " + str(demo_outfit._m1_Outfit__get_total_slut_modifiers()) style "serum_text_style_traits"

                                                # DEBUG CODE TO SEE WHAT WHAT IS STORED IN BASE OUTFIT (UI selection control)
                                                # frame:
                                                #     background "#143869"
                                                #     xsize 250
                                                #     padding (1,1)
                                                #     text "Base Outfit: [base_outfit.item_count]" style "serum_text_style_traits"

                                                # DEBUG CODE TO SEE WHAT IS SELECTED WHEN WE CLICK AROUND
                                                # frame:
                                                #     background "#43B197"
                                                #     xsize 250
                                                #     padding (1,1)
                                                #     if (selected_clothing):
                                                #         text "Selected Item: " + selected_clothing.name style "serum_text_style_traits"

                                frame:
                                    background "#0a142688"
                                    xsize 262
                                    vbox:
                                        frame:
                                            background "#143869"
                                            padding (1,1)
                                            xsize 250
                                            text "Visible Layers:" style "serum_text_style_traits"
                                        hbox:
                                            xfill True
                                            textbutton "Under":
                                                style "textbutton_no_padding_highlight"
                                                text_style "serum_text_style"
                                                xsize 62
                                                hover_background "#143869"
                                                background ("#171717" if hide_underwear else "#14386988")
                                                action [ToggleScreenVariable("hide_underwear", False, True), Function(preview_outfit)]
                                            textbutton "Shoe":
                                                style "textbutton_no_padding_highlight"
                                                text_style "serum_text_style"
                                                xsize 62
                                                hover_background "#143869"
                                                background ("#171717" if hide_shoes else "#14386988")
                                                action [ToggleScreenVariable("hide_shoes", False, True), Function(preview_outfit)]
                                            textbutton "Cloth":
                                                style "textbutton_no_padding_highlight"
                                                text_style "serum_text_style"
                                                xsize 62
                                                hover_background "#143869"
                                                background ("#171717" if hide_base else "#14386988")
                                                action [ToggleScreenVariable("hide_base", False, True), Function(preview_outfit)]
                                            textbutton "Over":
                                                style "textbutton_no_padding_highlight"
                                                text_style "serum_text_style"
                                                xsize 62
                                                hover_background "#143869"
                                                background ("#171717" if hide_overwear else "#14386988")
                                                action [ToggleScreenVariable("hide_overwear", False, True), Function(preview_outfit)]

                            vbox:
                                frame:
                                    background "#0a142688"

                                    xfill True
                                    viewport:
                                        scrollbars "vertical"
                                        mousewheel True
                                        xfill True
                                        vbox:
                                            spacing 5
                                            for cloth in (x for x in demo_outfit if not x.is_extension and not x.layer in hide_list):
                                                hbox:
                                                    button:
                                                        background Color(rgb = (cloth.colour[0], cloth.colour[1], cloth.colour[2]))

                                                        action [ # NOTE: Left click makes more sense for selection than right clicking
                                                            SetScreenVariable("category_selected", get_category(cloth)),
                                                            SetScreenVariable("selected_clothing", cloth),
                                                            SetScreenVariable("selected_colour", "colour"),

                                                            SetScreenVariable("current_r", cloth.colour[0]),
                                                            SetScreenVariable("current_g", cloth.colour[1]),
                                                            SetScreenVariable("current_b", cloth.colour[2]),
                                                            SetScreenVariable("current_a", cloth.colour[3]),

                                                            Function(preview_outfit) # Make sure it is showing the correct outfit during changes, demo_outfit is a copy of base_outfit
                                                        ]
                                                        alternate [
                                                            Function(hide_mannequin),
                                                            Function(base_outfit.remove_clothing, cloth),
                                                            Function(demo_outfit.remove_clothing, cloth),
                                                            Function(preview_outfit)
                                                        ]
                                                        xalign 0.5
                                                        xsize 230
                                                        right_padding 32
                                                        ysize 34
                                                        text "[cloth.name]" xalign 0.5 yalign 0.5 xfill True yoffset 2 style "custom_outfit_style"

                                                    imagebutton:
                                                        auto "gui/button/delete_%s.png"
                                                        focus_mask True
                                                        xoffset -32
                                                        #pos (230, 0)
                                                        action [
                                                            Function(hide_mannequin),
                                                            Function(base_outfit.remove_clothing, cloth),
                                                            Function(demo_outfit.remove_clothing, cloth),
                                                            Function(preview_outfit)
                                                        ]

            frame:
                background "#0a142688"

                xysize (540, 500)
                #padding (20,20)
                hbox:
                    spacing 5
                    vbox:
                        hbox:
                            spacing 5
                            vbox:
                                spacing 5
                                frame:
                                    background "#0a142688"
                                    xsize 250
                                    vbox:
                                        xalign 0.5

                                        $ enabled = True
                                        $ save_button_name = "Base Outfit Done" if outfit_type == "makeup" else "Save Outfit"
                                        if callable(outfit_validator):
                                            $ enabled = outfit_validator(demo_outfit, outfit_type)
                                            if enabled:
                                                $ save_button_name += " {menu_green=14}Valid{/menu_green}"
                                            else:
                                                $ save_button_name += " {menu_red=14}Invalid{/menu_red}"
                                        elif slut_limit is not None:
                                            $ save_button_name += f" {{menu_red=14}}Max: {min(slut_limit, 100)} slut{{/menu_red}}"
                                            $ enabled = get_slut_score() <= slut_limit

                                        textbutton "[save_button_name]":
                                            style "textbutton_no_padding_highlight"
                                            text_style "serum_text_style"
                                            xfill True
                                            sensitive enabled

                                            action [
                                                Function(update_outfit_name, demo_outfit),
                                                Function(hide_mannequin),
                                                Return(demo_outfit),
                                            ]

                                        if outfit_type != "makeup":
                                            textbutton "Abandon / Exit":
                                                style "textbutton_no_padding_highlight"
                                                text_style "serum_text_style"
                                                xfill True

                                                action [
                                                    Function(hide_mannequin),
                                                    Return(None),
                                                ]

                                frame:
                                    background "#0a142688"
                                    xsize 250
                                    vbox:
                                        xalign 0.5
                                        textbutton ("Export to [selected_xml]" if mannequin == "mannequin" else "Add to [mannequin.name] wardrobe"):
                                            style "textbutton_no_padding_highlight"
                                            text_style "serum_text_style"
                                            xfill True

                                            if mannequin == "mannequin":
                                                action [
                                                    Function(log_outfit, demo_outfit, outfit_class = outfit_type, wardrobe_name = selected_xml),
                                                    Function(renpy.notify, f"Outfit exported to {selected_xml}")
                                                ]

                                            else:
                                                if outfit_type == "full":
                                                    action [
                                                        Function(mannequin.wardrobe.add_outfit, demo_outfit),
                                                        Function(renpy.notify, f"Outfit added to {mannequin.name} wardrobe")
                                                    ]
                                                elif outfit_type == "over":
                                                    action [
                                                        Function(mannequin.wardrobe.add_overwear_set, demo_outfit),
                                                        Function(renpy.notify, f"Overwear added to {mannequin.name} wardrobe")
                                                    ]

                                                elif outfit_type == "under":
                                                    action [
                                                        Function(mannequin.wardrobe.add_underwear_set, demo_outfit),
                                                        Function(renpy.notify, f"Underwear added to {mannequin.name} wardrobe")
                                                    ]

                                if outfit_type != "makeup":
                                    frame:
                                        background "#0a142688"
                                        xsize 254
                                        vbox:
                                            textbutton "Generate [outfit_class_selected]":
                                                xfill True
                                                style "textbutton_no_padding_highlight"
                                                text_style "serum_text_style"
                                                tooltip "Generate random outfit based on clothing sluttiness values and selected girl."
                                                action [
                                                    Function(set_generated_outfit, outfit_type, slut_generation, min_slut_generation)
                                                ]

                                            textbutton "Personalize Outfit":
                                                xfill True
                                                style "textbutton_no_padding_highlight"
                                                text_style "serum_text_style"
                                                tooltip "Personalize outfit for selected girl."
                                                action [
                                                    Function(personalize_generated_outfit),
                                                    Function(update_color_selection, selected_clothing)
                                                ]

                                            hbox:
                                                button:
                                                    background "#505050"
                                                    text "Slut [slut_generation]" style "serum_text_style" yalign 0.5 size 16
                                                    xsize 90
                                                    ysize 24
                                                bar:
                                                    adjustment ui.adjustment(range = max_slut, value = slut_generation, step = 1, changed = update_slut_generation)
                                                    xfill True
                                                    ysize 24
                                                    thumb gold_heart
                                                    style style.slider

                                            if slut_generation > 0:
                                                hbox:
                                                    button:
                                                        background "#505050"
                                                        text "Min [min_slut_generation]" style "serum_text_style" yalign 0.5 size 16
                                                        xsize 90
                                                        ysize 24
                                                    bar:
                                                        adjustment ui.adjustment(range = (slut_generation if slut_generation < 4 else 4), value = min_slut_generation, step = 1, changed = update_min_slut_generation)
                                                        xfill True
                                                        ysize 24
                                                        thumb gold_heart
                                                        style style.slider


                                if isinstance(mannequin, Person):
                                    $ love_list = outfit_builder.person.loved_outfit_opinions + outfit_builder.person.loved_color_opinions
                                    $ hate_list = outfit_builder.person.hated_outfit_opinions + outfit_builder.person.hated_color_opinions
                                    frame:
                                        background "#0a142688"
                                        xsize 250
                                        vbox:
                                            spacing 0
                                            frame:
                                                background "#000080"
                                                xsize 240
                                                padding (1,1)
                                                text "Preferences:" style "serum_text_style_traits"
                                            viewport:
                                                scrollbars "vertical"
                                                draggable True
                                                mousewheel True
                                                yfill True
                                                xsize 240
                                                vbox:
                                                    if builtins.len(love_list) > 0:
                                                        for pref in love_list:
                                                            frame:
                                                                background "#43B197"
                                                                xsize 220
                                                                padding (1,1)
                                                                text "[pref]" style "serum_text_style_traits"
                                                    if builtins.len(hate_list) > 0:
                                                        for pref in hate_list:
                                                            frame:
                                                                background "#B14365"
                                                                xsize 220
                                                                padding (1,1)
                                                                text "[pref]" style "serum_text_style_traits"

                            vbox:

                                frame:
                                    background "#0a142688"
                                    xfill True
                                    vbox:
                                        if outfit_type != "makeup":
                                            textbutton "Import Design":
                                                style "textbutton_no_padding_highlight"
                                                text_style "serum_text_style"
                                                xfill True
                                                xalign 0.5

                                                if import_selection:
                                                    background "#4f7ad6"
                                                    hover_background "#4f7ad6"

                                                action [
                                                ToggleScreenVariable("import_selection"),
                                                If(mannequin_selection or mannequin_poser, [SetScreenVariable("mannequin_selection", False), SetScreenVariable("mannequin_poser", False)])
                                                ]

                                            textbutton "Mannequin Selection":
                                                style "textbutton_no_padding_highlight"
                                                text_style "serum_text_style"
                                                xfill True
                                                xalign 0.5

                                                if mannequin_selection:
                                                    background "#4f7ad6"
                                                    hover_background "#4f7ad6"

                                                action [
                                                ToggleScreenVariable("mannequin_selection"),
                                                If(import_selection or mannequin_poser, [SetScreenVariable("import_selection", False), SetScreenVariable("mannequin_poser", False)])
                                                ]

                                        textbutton "Mannequin Poser":
                                            style "textbutton_no_padding_highlight"
                                            text_style "serum_text_style"
                                            xfill True
                                            xalign 0.5
                                            if mannequin_poser:
                                                background "#4f7ad6"
                                                hover_background "#4f7ad6"


                                            action [
                                                SensitiveIf(mannequin != "mannequin"),
                                                ToggleScreenVariable("mannequin_poser"),
                                                If(import_selection or mannequin_selection, [SetScreenVariable("import_selection", False), SetScreenVariable("mannequin_selection", False)])
                                            ]

                                if import_selection:
                                    frame:
                                        background "#0a142688"
                                        xfill True
                                        viewport:
                                            scrollbars "vertical"
                                            mousewheel True
                                            draggable True
                                            vbox:
                                                for n in get_xml_files_from_path():
                                                    textbutton "[n]":
                                                        style "textbutton_no_padding_highlight"
                                                        text_style "serum_text_style"
                                                        xfill True
                                                        xalign 0.5

                                                        if selected_xml == n:
                                                            background "#4f7ad6"
                                                            hover_background "#4f7ad6"

                                                        action [
                                                            Show("import_outfit_manager", None, target_wardrobe, n, outfit_type)
                                                        ]
                                                        alternate [ #Right clicking selects the path that outfits should be exported to
                                                        SetVariable("selected_xml", n)
                                                        ]

                                if mannequin_selection:
                                    frame:
                                        background "#0a142688"
                                        xfill True
                                        viewport:
                                            scrollbars "vertical"
                                            mousewheel True
                                            draggable True
                                            vbox:
                                                textbutton "Default Mannequin":
                                                    style "textbutton_no_padding_highlight"
                                                    text_style "serum_text_style"
                                                    xfill True
                                                    xalign 0.5

                                                    action [
                                                        Function(switch_to_mannequin, None),
                                                        Function(preview_outfit)
                                                    ]
                                                for person in sorted(known_people_in_the_game(), key = lambda x: x.name):
                                                    textbutton ("[person.name] {image=full_star_token_small}" if person.is_favourite else "[person.name]"):
                                                        style "textbutton_no_padding_highlight"
                                                        text_style "serum_text_style"
                                                        xfill True
                                                        xalign 0.5

                                                        if mannequin == person:
                                                            background "#4f7ad6"
                                                            hover_background "#4f7ad6"

                                                        action [
                                                            Function(switch_to_mannequin, person),
                                                            Function(preview_outfit)
                                                        ]

                                if mannequin_poser:
                                    frame:
                                        background "#0a142688"
                                        xfill True
                                        viewport:
                                            scrollbars "vertical"
                                            mousewheel True
                                            draggable True
                                            vbox:
                                                for x in sorted(["stand2","stand3","stand4","stand5","walking_away","kissing","kneeling1","doggy","missionary","blowjob","against_wall","back_peek","sitting","standing_doggy","cowgirl"]):
                                                    textbutton "[x]":
                                                        style "textbutton_no_padding_highlight"
                                                        text_style "serum_text_style"
                                                        xfill True
                                                        xalign 0.5

                                                        if mannequin_pose == x:
                                                            background "#4f7ad6"
                                                            hover_background "#4f7ad6"

                                                        action [
                                                        SetScreenVariable("mannequin_pose", x),
                                                        Function(preview_outfit)
                                                        ]

                                                        alternate NullAction()

    imagebutton:
        auto "/tutorial_images/restart_tutorial_%s.png"
        xsize 54
        ysize 54
        yanchor 1.0
        xanchor 1.0
        xalign 1.0
        yalign 1.0
        action Function(mc.business.reset_tutorial,"outfit_tutorial")

    if mc.business.event_triggers_dict.get("outfit_tutorial", 0) > 0 and mc.business.event_triggers_dict.get("outfit_tutorial", 1) <= 8: #We use negative numbers to symbolize the tutorial not being enabled
        imagebutton:
            sensitive True
            xsize 1920
            ysize 1080
            idle f"/tutorial_images/outfit_tutorial_{mc.business.event_triggers_dict.get('outfit_tutorial', 1)}.png"
            hover f"/tutorial_images/outfit_tutorial_{mc.business.event_triggers_dict.get('outfit_tutorial', 1)}.png"
            action Function(mc.business.advance_tutorial,"outfit_tutorial")

    use default_tooltip("outfit_creator")
